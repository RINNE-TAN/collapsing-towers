
import CollapsingTowers.TwoLevelPCP.Syntax
import CollapsingTowers.TwoLevelPCP.Store
import CollapsingTowers.TwoLevelPCP.OpenClose
import CollapsingTowers.TwoLevelPCP.Env
abbrev Ctx :=
  Expr â†’ Expr

theorem ctx_comp : (f g : Ctx) â†’ âˆ€ e, f (g e) = (f âˆ˜ g) e := by simp

theorem ctx_swap : (f : Ctx) â†’ âˆ€ e, f (id e) = id (f e) := by simp

notation:max a "âŸ¦" b "âŸ§" => a b

inductive value : Expr â†’ Prop where
  | lam : âˆ€ e, lc (.lam e) â†’ value (.lam e)
  | lit : âˆ€ n, value (.lit n)
  | unit : value .unit
  | code : âˆ€ e, lc e â†’ value (.code e)
  | loc : âˆ€ l, value (.loc l)

inductive ctxğ”¹ : Ctx â†’ Prop where
  | applâ‚ : âˆ€ arg, lc arg â†’ ctxğ”¹ (fun X => .appâ‚ X arg)
  | apprâ‚ : âˆ€ v, value v â†’ ctxğ”¹ (fun X => .appâ‚ v X)
  | applâ‚‚ : âˆ€ arg, lc arg â†’ ctxğ”¹ (fun X => .appâ‚‚ X arg)
  | apprâ‚‚ : âˆ€ v, value v â†’ ctxğ”¹ (fun X => .appâ‚‚ v X)
  | binarylâ‚ : âˆ€ op r, lc r â†’ ctxğ”¹ (fun X => .binaryâ‚ op X r)
  | binaryrâ‚ : âˆ€ op v, value v â†’ ctxğ”¹ (fun X => .binaryâ‚ op v X)
  | binarylâ‚‚ : âˆ€ op r, lc r â†’ ctxğ”¹ (fun X => .binaryâ‚‚ op X r)
  | binaryrâ‚‚ : âˆ€ op v, value v â†’ ctxğ”¹ (fun X => .binaryâ‚‚ op v X)
  | lift : ctxğ”¹ (fun X => .lift X)
  | lets : âˆ€ e, closedb_at e 1 â†’ ctxğ”¹ (fun X => .lets X e)
  | loadâ‚ : ctxğ”¹ (fun X => .loadâ‚ X)
  | allocâ‚ : ctxğ”¹ (fun X => .allocâ‚ X)
  | storelâ‚ : âˆ€ r, lc r â†’ ctxğ”¹ (fun X => .storeâ‚ X r)
  | storerâ‚ : âˆ€ v, value v â†’ ctxğ”¹ (fun X => .storeâ‚ v X)
  | loadâ‚‚ : ctxğ”¹ (fun X => .loadâ‚‚ X)
  | allocâ‚‚ : ctxğ”¹ (fun X => .allocâ‚‚ X)
  | storelâ‚‚ : âˆ€ r, lc r â†’ ctxğ”¹ (fun X => .storeâ‚‚ X r)
  | storerâ‚‚ : âˆ€ v, value v â†’ ctxğ”¹ (fun X => .storeâ‚‚ v X)
  | ifzâ‚ : âˆ€ l r, lc l â†’ lc r â†’ ctxğ”¹ (fun X => .ifzâ‚ X l r)
  | ifzâ‚‚ : âˆ€ l r, lc l â†’ lc r â†’ ctxğ”¹ (fun X => .ifzâ‚‚ X l r)
  | fixâ‚ : ctxğ”¹ (fun X => .fixâ‚ X)
  | fixâ‚‚ : ctxğ”¹ (fun X => .fixâ‚‚ X)

inductive ctxâ„ : â„• â†’ â„• â†’ Ctx â†’ Prop where
  | lamğ•” : ctxâ„ 1 lvl (fun X => .lamğ•” (closeâ‚€ lvl X))
  | letğ•” : âˆ€ b, lc b â†’ ctxâ„ 1 lvl (fun X => .letğ•” b (closeâ‚€ lvl X))
  | run : ctxâ„ 0 lvl (fun X => .run X)
  | ifzlâ‚‚ : âˆ€ v r, value v â†’ lc r â†’ ctxâ„ 0 lvl (fun X => .ifzâ‚‚ v X r)
  | ifzrâ‚‚ : âˆ€ vâ‚€ vâ‚, value vâ‚€ â†’ value vâ‚ â†’ ctxâ„ 0 lvl (fun X => .ifzâ‚‚ vâ‚€ vâ‚ X)

inductive ctxğ•„ : â„• â†’ Ctx â†’ Prop where
  | hole : ctxğ•„ lvl id
  | consğ”¹ : âˆ€ B M, ctxğ”¹ B â†’ ctxğ•„ lvl M â†’ ctxğ•„ lvl (B âˆ˜ M)
  | consâ„ : âˆ€ R M, ctxâ„ intro lvl R â†’ ctxğ•„ (lvl + intro) M â†’ ctxğ•„ lvl (R âˆ˜ M)

inductive ctxğ”¼ : Ctx â†’ Prop where
  | hole : ctxğ”¼ id
  | consğ”¹ : âˆ€ B E, ctxğ”¹ B â†’ ctxğ”¼ E â†’ ctxğ”¼ (B âˆ˜ E)

inductive ctxâ„š : â„• â†’ Ctx â†’ Prop where
  | holeâ„ : âˆ€ R, ctxâ„ intro lvl R â†’ ctxâ„š lvl R
  | consğ”¹ : âˆ€ B Q, ctxğ”¹ B â†’ ctxâ„š lvl Q â†’ ctxâ„š lvl (B âˆ˜ Q)
  | consâ„ : âˆ€ R Q, ctxâ„ intro lvl R â†’ ctxâ„š (lvl + intro) Q â†’ ctxâ„š lvl (R âˆ˜ Q)

inductive ctxâ„™ : â„• â†’ Ctx â†’ Prop where
  | hole : ctxâ„™ lvl id
  | consâ„š : âˆ€ Q, ctxâ„š lvl Q â†’ ctxâ„™ lvl Q

mutual
  inductive ctxâ„™' : â„• â†’ Ctx â†’ Prop where
    | hole : ctxâ„™' lvl id
    | consğ”¹ : âˆ€ B Q, ctxğ”¹ B â†’ ctxâ„š' lvl Q â†’ ctxâ„™' lvl (B âˆ˜ Q)
    | consâ„ : âˆ€ R P, ctxâ„ intro lvl R â†’ ctxâ„™' (lvl + intro) P â†’ ctxâ„™' lvl (R âˆ˜ P)
  inductive ctxâ„š' : â„• â†’ Ctx â†’ Prop where
    | consğ”¹ : âˆ€ B Q, ctxğ”¹ B â†’ ctxâ„š' lvl Q â†’ ctxâ„š' lvl (B âˆ˜ Q)
    | consâ„ : âˆ€ R P, ctxâ„ intro lvl R â†’ ctxâ„™' (lvl + intro) P â†’ ctxâ„š' lvl (R âˆ˜ P)
end

theorem ctxâ„™_iff_ctxâ„™' : âˆ€ P lvl, ctxâ„™' lvl P â†” ctxâ„™ lvl P :=
  by
  intros C lvl
  constructor
  . intro HP
    apply
      @ctxâ„™'.rec
        (fun lvl P (H : ctxâ„™' lvl P) => ctxâ„™ lvl P)
        (fun lvl P (H : ctxâ„š' lvl P) => ctxâ„š lvl P)
    case hole => apply ctxâ„™.hole
    case consğ”¹ =>
      intros _ _ _ HB _ IHQ
      apply ctxâ„™.consâ„š
      apply ctxâ„š.consğ”¹
      apply HB; apply IHQ
    case consâ„ =>
      intros _ _ _ _ HR _ IHP
      apply ctxâ„™.consâ„š
      cases IHP with
      | hole =>
        apply ctxâ„š.holeâ„
        apply HR
      | consâ„š _ HQ =>
        apply ctxâ„š.consâ„
        apply HR; apply HQ
    case consğ”¹ =>
      intros _ _ _ HB _ IHQ
      apply ctxâ„š.consğ”¹
      apply HB; apply IHQ
    case consâ„ =>
      intros _ _ _ _ HR _ IHP
      cases IHP with
      | hole =>
        apply ctxâ„š.holeâ„
        apply HR
      | consâ„š _ HQ =>
        apply ctxâ„š.consâ„
        apply HR; apply HQ
    apply HP
  . intro HP
    cases HP
    case hole =>
      apply ctxâ„™'.hole
    case consâ„š HQ =>
      have H :
        (âˆƒ B Q, ctxğ”¹ B âˆ§ ctxâ„š' lvl Q âˆ§ C = B âˆ˜ Q) âˆ¨
        (âˆƒ R P intro, ctxâ„ intro lvl R âˆ§ ctxâ„™' (lvl + intro) P âˆ§ C = R âˆ˜ P) :=
        by
        induction HQ with
        | holeâ„ R HR =>
          right
          exists R, id
          constructor; constructor
          apply HR; constructor
          apply ctxâ„™'.hole; rfl
        | consğ”¹ Bâ‚€ Qâ‚€ HBâ‚€ _ IHQ =>
          left; exists Bâ‚€, Qâ‚€
          cases IHQ
          case inl IHQ =>
            have âŸ¨Bâ‚, Qâ‚, HBâ‚, HQâ‚, HEqCâŸ© := IHQ; constructor
            apply HBâ‚€; constructor
            rw [HEqC]; apply ctxâ„š'.consğ”¹
            apply HBâ‚; apply HQâ‚; rfl
          case inr IHQ =>
            have âŸ¨R, P, _, HR, HP, HEqCâŸ© := IHQ; constructor
            apply HBâ‚€; constructor
            rw [HEqC]; apply ctxâ„š'.consâ„
            apply HR; apply HP; rfl
        | consâ„ Râ‚€ Pâ‚€ HRâ‚€ _ IHP =>
          right; exists Râ‚€, Pâ‚€
          cases IHP
          case inl IHP =>
            have âŸ¨B, Q, HB, HQ, HEqCâŸ© := IHP
            constructor; constructor
            apply HRâ‚€; constructor
            rw [HEqC]; apply ctxâ„™'.consğ”¹
            apply HB; apply HQ; rfl
          case inr IHQ =>
            have âŸ¨Râ‚, Pâ‚, _, HRâ‚, HPâ‚, HEqCâŸ© := IHQ
            constructor; constructor
            apply HRâ‚€; constructor
            rw [HEqC]; apply ctxâ„™'.consâ„
            apply HRâ‚; apply HPâ‚; rfl
      cases H
      case inl H =>
        have âŸ¨B, Q, HB, HQ, HEqCâŸ© := H
        rw [HEqC]; apply ctxâ„™'.consğ”¹
        apply HB; apply HQ
      case inr H =>
        have âŸ¨R, P, _, HR, HP, HEqCâŸ© := H
        rw [HEqC]; apply ctxâ„™'.consâ„
        apply HR; apply HP

theorem value_lc : âˆ€ e, value e â†’ lc e := by
  intro e Hvalue
  cases Hvalue with
  | lam _ Hclose => apply Hclose
  | lit => constructor
  | unit => constructor
  | code _ Hclose => apply Hclose
  | loc => constructor

-- properties of ğ”¹ contexts

theorem lc_ctxğ”¹ : âˆ€ B e n, ctxğ”¹ B â†’ closedb_at e n â†’ closedb_at BâŸ¦eâŸ§ n :=
  by
  intros _ _ _ HB Hlc
  induction HB with
  | applâ‚ _ IH
  | applâ‚‚ _ IH
  | binarylâ‚ _ _ IH
  | binarylâ‚‚ _ _ IH
  | lets _ IH
  | storelâ‚ _ IH
  | storelâ‚‚ _ IH =>
    constructor; apply Hlc
    apply closedb_inc; apply IH; omega
  | apprâ‚ _ Hvalue
  | apprâ‚‚ _ Hvalue
  | binaryrâ‚ _ _ Hvalue
  | binaryrâ‚‚ _ _ Hvalue
  | storerâ‚ _ Hvalue
  | storerâ‚‚ _ Hvalue =>
    constructor
    apply closedb_inc; apply value_lc; apply Hvalue; omega
    apply Hlc
  | lift| loadâ‚| allocâ‚| loadâ‚‚| allocâ‚‚| fixâ‚| fixâ‚‚ => apply Hlc
  | ifzâ‚ _ _ IHâ‚€ IHâ‚
  | ifzâ‚‚ _ _ IHâ‚€ IHâ‚ =>
    constructor; apply Hlc
    constructor
    apply closedb_inc; apply IHâ‚€; omega
    apply closedb_inc; apply IHâ‚; omega

theorem closed_at_decomposeğ”¹ : âˆ€ B eâ‚€ x, ctxğ”¹ B â†’ closed_at BâŸ¦eâ‚€âŸ§ x â†’ closed_at eâ‚€ x :=
  by
  intros _ _ _ HB Hclose
  cases HB with
  | applâ‚| applâ‚‚| binarylâ‚| binarylâ‚‚| lets| storelâ‚| storelâ‚‚ =>
    apply Hclose.left
  | apprâ‚| apprâ‚‚| binaryrâ‚| binaryrâ‚‚| storerâ‚| storerâ‚‚ =>
    apply Hclose.right
  | lift| loadâ‚| allocâ‚| loadâ‚‚| allocâ‚‚| fixâ‚| fixâ‚‚ => apply Hclose
  | ifzâ‚| ifzâ‚‚ => apply Hclose.left

theorem closed_atğ”¹ : âˆ€ B eâ‚€ eâ‚ x, ctxğ”¹ B â†’ closed_at BâŸ¦eâ‚€âŸ§ x â†’ closed_at eâ‚ x â†’ closed_at BâŸ¦eâ‚âŸ§ x :=
  by
  intros _ _ _ _ HB Heâ‚€ Heâ‚
  cases HB with
  | applâ‚| applâ‚‚| binarylâ‚| binarylâ‚‚| lets| storelâ‚| storelâ‚‚ =>
    constructor; apply Heâ‚; apply Heâ‚€.right
  | apprâ‚| apprâ‚‚| binaryrâ‚| binaryrâ‚‚| storerâ‚| storerâ‚‚ =>
    constructor; apply Heâ‚€.left; apply Heâ‚
  | lift| loadâ‚| allocâ‚| loadâ‚‚| allocâ‚‚| fixâ‚| fixâ‚‚ => apply Heâ‚
  | ifzâ‚| ifzâ‚‚ => constructor; apply Heâ‚; apply Heâ‚€.right

theorem fv_atğ”¹ :
  âˆ€ B eâ‚€ eâ‚,
    ctxğ”¹ B â†’
    fv eâ‚ âŠ† fv eâ‚€ â†’
    fv BâŸ¦eâ‚âŸ§ âŠ† fv BâŸ¦eâ‚€âŸ§ :=
  by
  intros B eâ‚€ eâ‚ HB Hsubst
  cases HB with
  | applâ‚| applâ‚‚| binarylâ‚| binarylâ‚‚| lets| storelâ‚| storelâ‚‚ =>
    apply Set.union_subset_union
    apply Hsubst; rfl
  | apprâ‚| apprâ‚‚| binaryrâ‚| binaryrâ‚‚| storerâ‚| storerâ‚‚ =>
    apply Set.union_subset_union
    rfl; apply Hsubst
  | lift| loadâ‚| allocâ‚| loadâ‚‚| allocâ‚‚| fixâ‚| fixâ‚‚ => apply Hsubst
  | ifzâ‚| ifzâ‚‚ =>
    apply Set.union_subset_union
    apply Set.union_subset_union
    apply Hsubst; rfl; rfl

theorem fv_decomposeğ”¹ : âˆ€ B e, ctxğ”¹ B â†’ fv e âŠ† fv BâŸ¦eâŸ§ :=
  by
  intros _ _ HB
  cases HB <;> simp
  case ifzâ‚| ifzâ‚‚ =>
    rw [Set.union_assoc]; simp

theorem open_ctxğ”¹_map : âˆ€ B e x, ctxğ”¹ B â†’ openâ‚€ x BâŸ¦eâŸ§ = BâŸ¦openâ‚€ x eâŸ§ :=
  by
  intros B e x HB
  cases HB with
  | applâ‚ _ IH
  | applâ‚‚ _ IH
  | binarylâ‚ _ _ IH
  | binarylâ‚‚ _ _ IH
  | lets _ IH
  | storelâ‚ _ IH
  | storelâ‚‚ _ IH => simp; apply closedb_opening_id; apply IH
  | apprâ‚ _ Hvalue
  | apprâ‚‚ _ Hvalue
  | binaryrâ‚ _ _ Hvalue
  | binaryrâ‚‚ _ _ Hvalue
  | storerâ‚ _ Hvalue
  | storerâ‚‚ _ Hvalue => simp; apply closedb_opening_id; apply value_lc; apply Hvalue
  | lift| loadâ‚| allocâ‚| loadâ‚‚| allocâ‚‚| fixâ‚| fixâ‚‚ => simp
  | ifzâ‚ _ _ IHâ‚€ IHâ‚
  | ifzâ‚‚ _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply closedb_opening_id; apply IHâ‚€
    apply closedb_opening_id; apply IHâ‚

theorem substğ”¹ : âˆ€ B eâ‚€ eâ‚ v x, ctxğ”¹ B â†’ closed_at BâŸ¦eâ‚€âŸ§ x â†’ subst x v BâŸ¦eâ‚âŸ§ = BâŸ¦subst x v eâ‚âŸ§ :=
  by
  intros _ _ _ _ _ HB Heâ‚€
  cases HB with
  | applâ‚| applâ‚‚| binarylâ‚| binarylâ‚‚| lets| storelâ‚| storelâ‚‚ =>
    simp; apply subst_closed_id; apply Heâ‚€.right
  | apprâ‚| apprâ‚‚| binaryrâ‚| binaryrâ‚‚| storerâ‚| storerâ‚‚ =>
    simp; apply subst_closed_id; apply Heâ‚€.left
  | lift| loadâ‚| allocâ‚| loadâ‚‚| allocâ‚‚| fixâ‚| fixâ‚‚ => simp
  | ifzâ‚| ifzâ‚‚ =>
    simp; constructor
    apply subst_closed_id; apply Heâ‚€.right.left
    apply subst_closed_id; apply Heâ‚€.right.right

-- properties of â„ contexts

theorem lc_ctxâ„ : âˆ€ R e n intro lvl, ctxâ„ intro lvl R â†’ closedb_at e n â†’ closedb_at RâŸ¦eâŸ§ n :=
  by
  intros _ _ _ _ _ HR Hlc
  cases HR with
  | lamğ•” =>
    apply close_closedb; omega
    apply closedb_inc; apply Hlc; omega
  | letğ•” _ Hlcb =>
    constructor
    apply closedb_inc; apply Hlcb; omega
    apply close_closedb; omega
    apply closedb_inc; apply Hlc; omega
  | run =>
    apply Hlc
  | ifzlâ‚‚ _ _ Hvalue Hlcr =>
    constructor; apply closedb_inc; apply value_lc; apply Hvalue; omega
    constructor; apply closedb_inc; apply Hlc; omega
    apply closedb_inc; apply Hlcr; omega
  | ifzrâ‚‚ _ _ Hvalueâ‚€ Hvalueâ‚ =>
    constructor; apply closedb_inc; apply value_lc; apply Hvalueâ‚€; omega
    constructor; apply closedb_inc; apply value_lc; apply Hvalueâ‚; omega
    apply closedb_inc; apply Hlc; omega

theorem fv_atâ„ :
  âˆ€ intro lvl R eâ‚€ eâ‚,
    ctxâ„ intro lvl R â†’
    fv eâ‚ âŠ† fv eâ‚€ â†’
    fv RâŸ¦eâ‚âŸ§ âŠ† fv RâŸ¦eâ‚€âŸ§ :=
  by
  intros intro lvl R eâ‚€ eâ‚ HR Hsubst
  cases HR with
  | lamğ•” =>
    simp
    rw [fv_closing, fv_closing]
    apply Set.diff_subset_diff_left
    apply Hsubst
  | letğ•” =>
    simp
    rw [fv_closing, fv_closing]
    apply Set.subset_union_of_subset_right
    apply Set.diff_subset_diff_left
    apply Hsubst
  | run => apply Hsubst
  | ifzlâ‚‚ =>
    simp; constructor
    rw [Set.union_assoc]; simp
    apply Set.subset_union_of_subset_left
    apply Set.subset_union_of_subset_right
    apply Hsubst
  | ifzrâ‚‚ =>
    simp
    apply Set.subset_union_of_subset_right
    apply Hsubst

-- properties of ğ•„ contexts

theorem lc_ctxğ•„ : âˆ€ M e n lvl, ctxğ•„ lvl M â†’ closedb_at e n â†’ closedb_at MâŸ¦eâŸ§ n :=
  by
  intros _ _ _ _ HM Hlc
  induction HM with
  | hole => apply Hlc
  | consğ”¹ _ _ HB _ IHlc => simp; apply lc_ctxğ”¹; apply HB; apply IHlc
  | consâ„ _ _ HR _ IHlc => simp; apply lc_ctxâ„; apply HR; apply IHlc

theorem fv_atğ•„ :
  âˆ€ lvl M eâ‚€ eâ‚,
    ctxğ•„ lvl M â†’
    fv eâ‚ âŠ† fv eâ‚€ â†’
    fv MâŸ¦eâ‚âŸ§ âŠ† fv MâŸ¦eâ‚€âŸ§ :=
  by
  intros lvl M eâ‚€ eâ‚ HM Hsubst
  induction HM with
  | hole => apply Hsubst
  | consğ”¹ _ _ HB _ IH =>
    simp; apply fv_atğ”¹
    apply HB; apply IH
  | consâ„ _ _ HR _ IH =>
    simp; apply fv_atâ„
    apply HR; apply IH

-- properties of ğ”¼ contexts

theorem lc_ctxğ”¼ : âˆ€ E e n, ctxğ”¼ E â†’ closedb_at e n â†’ closedb_at EâŸ¦eâŸ§ n :=
  by
  intros _ _ _ HE Hlc
  induction HE with
  | hole => apply Hlc
  | consğ”¹ _ _ HB _ IHlc => simp; apply lc_ctxğ”¹; apply HB; apply IHlc

theorem closed_at_decomposeğ”¼ : âˆ€ E eâ‚€ x, ctxğ”¼ E â†’ closed_at EâŸ¦eâ‚€âŸ§ x â†’ closed_at eâ‚€ x :=
  by
  intros _ _ _ HE Hclose
  induction HE with
  | hole => apply Hclose
  | consğ”¹ _ _ HB _ IH =>
    apply IH; apply closed_at_decomposeğ”¹
    apply HB; apply Hclose

theorem closed_atğ”¼ : âˆ€ E eâ‚€ eâ‚ x, ctxğ”¼ E â†’ closed_at EâŸ¦eâ‚€âŸ§ x â†’ closed_at eâ‚ x â†’ closed_at EâŸ¦eâ‚âŸ§ x :=
  by
  intros _ _ _ _ HE Heâ‚€ Heâ‚
  induction HE with
  | hole => apply Heâ‚
  | consğ”¹ _ _ HB _ IH =>
    simp; apply closed_atğ”¹; apply HB; apply Heâ‚€
    apply IH; apply closed_at_decomposeğ”¹; apply HB; apply Heâ‚€

theorem fv_atğ”¼ :
  âˆ€ E eâ‚€ eâ‚,
    ctxğ”¼ E â†’
    fv eâ‚ âŠ† fv eâ‚€ â†’
    fv EâŸ¦eâ‚âŸ§ âŠ† fv EâŸ¦eâ‚€âŸ§ :=
  by
  intros E eâ‚€ eâ‚ HE Hsubst
  induction HE with
  | hole => apply Hsubst
  | consğ”¹ _ _ HB _ IH =>
    simp; apply fv_atğ”¹
    apply HB; apply IH

theorem fv_decomposeğ”¼ : âˆ€ E e, ctxğ”¼ E â†’ fv e âŠ† fv EâŸ¦eâŸ§ :=
  by
  intros _ _ HE
  induction HE with
  | hole => rfl
  | consğ”¹ _ _ HB _ IH =>
    apply Set.Subset.trans; apply IH
    apply fv_decomposeğ”¹; apply HB

theorem open_ctxğ”¼_map : âˆ€ E e x, ctxğ”¼ E â†’ openâ‚€ x EâŸ¦eâŸ§ = EâŸ¦openâ‚€ x eâŸ§ :=
  by
  intros _ _ _ HE
  induction HE with
  | hole => rfl
  | consğ”¹ _ _ HB _ IH =>
    simp at *; rw [â† IH]
    apply open_ctxğ”¹_map; apply HB

theorem substğ”¼ : âˆ€ E eâ‚€ eâ‚ v x, ctxğ”¼ E â†’ closed_at EâŸ¦eâ‚€âŸ§ x â†’ subst x v EâŸ¦eâ‚âŸ§ = EâŸ¦subst x v eâ‚âŸ§ :=
  by
  intros _ _ _ _ _ HE Heâ‚€
  induction HE with
  | hole => simp
  | consğ”¹ _ E HB _ IH =>
    simp at *; rw [â† IH]; apply substğ”¹
    apply HB; apply Heâ‚€
    cases HB with
    | applâ‚| applâ‚‚| binarylâ‚| binarylâ‚‚| lets| storelâ‚| storelâ‚‚ => apply Heâ‚€.left
    | apprâ‚| apprâ‚‚| binaryrâ‚| binaryrâ‚‚| storerâ‚| storerâ‚‚ => apply Heâ‚€.right
    | lift| loadâ‚| allocâ‚| loadâ‚‚| allocâ‚‚| fixâ‚| fixâ‚‚ => apply Heâ‚€
    | ifzâ‚| ifzâ‚‚ => apply Heâ‚€.left

-- properties of â„š contexts

theorem lc_ctxâ„š : âˆ€ Q e n lvl, ctxâ„š lvl Q â†’ closedb_at e n â†’ closedb_at QâŸ¦eâŸ§ n :=
  by
  intros _ _ _ _ HQ Hlc
  induction HQ with
  | holeâ„ _ HR => apply lc_ctxâ„; apply HR; apply Hlc
  | consğ”¹ _ _ HB _ IHlc => simp; apply lc_ctxğ”¹; apply HB; apply IHlc
  | consâ„ _ _ HR _ IHlc => simp; apply lc_ctxâ„; apply HR; apply IHlc

theorem fv_atâ„š :
  âˆ€ lvl Q eâ‚€ eâ‚,
    ctxâ„š lvl Q â†’
    fv eâ‚ âŠ† fv eâ‚€ â†’
    fv QâŸ¦eâ‚âŸ§ âŠ† fv QâŸ¦eâ‚€âŸ§ :=
  by
  intros lvl Q eâ‚€ eâ‚ HQ Hsubst
  induction HQ with
  | holeâ„ _ HR =>
    apply fv_atâ„
    apply HR; apply Hsubst
  | consğ”¹ _ _ HB _ IH =>
    simp; apply fv_atğ”¹
    apply HB; apply IH
  | consâ„ _ _ HR _ IH =>
    simp; apply fv_atâ„
    apply HR; apply IH

inductive headğ•„ : Expr â†’ Expr â†’ Prop where
  | lets : âˆ€ e v, value v â†’ headğ•„ (.lets v e) (open_subst v e)
  | appâ‚ : âˆ€ e v, value v â†’ headğ•„ (.appâ‚ (.lam e) v) (open_subst v e)
  | appâ‚‚ : âˆ€ f arg, headğ•„ (.appâ‚‚ (.code f) (.code arg)) (.reflect (.appâ‚ f arg))
  | binaryâ‚ : âˆ€ op l r, headğ•„ (.binaryâ‚ op (.lit l) (.lit r)) (.lit (eval op l r))
  | binaryâ‚‚ : âˆ€ op l r, headğ•„ (.binaryâ‚‚ op (.code l) (.code r)) (.reflect (.binaryâ‚ op l r))
  | lift_lit : âˆ€ n, headğ•„ (.lift (.lit n)) (.reflect (.lit n))
  | lift_unit : headğ•„ (.lift .unit) (.reflect .unit)
  | lift_lam : âˆ€ e, headğ•„ (.lift (.lam e)) (.lamğ•” (mapğ•”â‚€ e))
  | lamğ•” : âˆ€ e, headğ•„ (.lamğ•” (.code e)) (.reflect (.lam e))
  | letğ•” : âˆ€ b e, headğ•„ (.letğ•” b (.code e)) (.code (.lets b e))
  | run : âˆ€ e, headğ•„ (.run (.code e)) e
  | loadâ‚‚ : âˆ€ e, headğ•„ (.loadâ‚‚ (.code e)) (.reflect (.loadâ‚ e))
  | allocâ‚‚ : âˆ€ e, headğ•„ (.allocâ‚‚ (.code e)) (.reflect (.allocâ‚ e))
  | storeâ‚‚ : âˆ€ l r, headğ•„ (.storeâ‚‚ (.code l) (.code r)) (.reflect (.storeâ‚ l r))
  | ifzâ‚_left : âˆ€ l r, headğ•„ (.ifzâ‚ (.lit 0) l r) l
  | ifzâ‚_right : âˆ€ l r n, headğ•„ (.ifzâ‚ (.lit (.succ n)) l r) r
  | ifzâ‚‚ : âˆ€ c l r, headğ•„ (.ifzâ‚‚ (.code c) (.code l) (.code r)) (.reflect (.ifzâ‚ c l r))
  | fixâ‚ : âˆ€ e, headğ•„ (.fixâ‚ (.lam e)) (open_subst (.fixâ‚ (.lam e)) e)
  | fixâ‚‚ : âˆ€ e, headğ•„ (.fixâ‚‚ (.code e)) (.reflect (.fixâ‚ e))

inductive sheadğ•„ : (Store Ã— Expr) â†’ (Store Ã— Expr) â†’ Prop where
  | loadâ‚ : âˆ€ st l e, binds l e st â†’ sheadğ•„ (st, .loadâ‚ (.loc l)) (st, e)
  | allocâ‚ : âˆ€ st v, value v â†’ sheadğ•„ (st, .allocâ‚ v) (v :: st, .loc (st.length))
  | storeâ‚ : âˆ€ stâ‚€ stâ‚ l v, value v â†’ patch l v stâ‚€ stâ‚ â†’ sheadğ•„ (stâ‚€, .storeâ‚ (.loc l) v) (stâ‚, .unit)

inductive step_lvl (lvl : â„•) : (Store Ã— Expr) â†’ (Store Ã— Expr) â†’ Prop where
  | stepğ•„ : âˆ€ M eâ‚€ eâ‚ st, ctxğ•„ lvl M â†’ lc eâ‚€ â†’ headğ•„ eâ‚€ eâ‚ â†’ step_lvl lvl (st, MâŸ¦eâ‚€âŸ§) (st, MâŸ¦eâ‚âŸ§)
  | storeğ•„ : âˆ€ M stâ‚€ stâ‚ eâ‚€ eâ‚, ctxğ•„ lvl M â†’ lc eâ‚€ â†’ sheadğ•„ (stâ‚€, eâ‚€) (stâ‚, eâ‚) â†’ step_lvl lvl (stâ‚€, MâŸ¦eâ‚€âŸ§) (stâ‚, MâŸ¦eâ‚âŸ§)
  | reflect : âˆ€ P E b st, ctxâ„™ lvl P â†’ ctxğ”¼ E â†’ lc b â†’ step_lvl lvl (st, PâŸ¦EâŸ¦.reflect bâŸ§âŸ§) (st, PâŸ¦.letğ•” b EâŸ¦.code (.bvar 0)âŸ§âŸ§)

@[simp]
def step : (Store Ã— Expr) â†’ (Store Ã— Expr) â†’ Prop :=
  step_lvl 0

inductive stepn : (Store Ã— Expr) â†’ (Store Ã— Expr) â†’ Prop
  | refl : âˆ€ e, stepn e e
  | multi : âˆ€ eâ‚€ eâ‚ eâ‚‚, stepn eâ‚€ eâ‚ â†’ step eâ‚ eâ‚‚ â†’ stepn eâ‚€ eâ‚‚

-- properties of step

theorem stepğ”¹ : âˆ€ lvl B stâ‚€ stâ‚ eâ‚€ eâ‚, ctxğ”¹ B â†’ step_lvl lvl (stâ‚€, eâ‚€) (stâ‚, eâ‚) â†’ âˆƒ eâ‚‚, step_lvl lvl (stâ‚€, BâŸ¦eâ‚€âŸ§) (stâ‚, eâ‚‚) :=
  by
  intros lvl B stâ‚€ stâ‚ eâ‚€ eâ‚ HB Hstep
  cases Hstep with
  | stepğ•„ M _ _ _ HM Hlc Hhead =>
    rw [ctx_comp B M]
    constructor; apply step_lvl.stepğ•„
    apply ctxğ•„.consğ”¹; apply HB; apply HM
    apply Hlc; apply Hhead
  | storeğ•„ M _ _ _ _ HM Hlc Hstore =>
    rw [ctx_comp B M]
    constructor; apply step_lvl.storeğ•„
    apply ctxğ•„.consğ”¹; apply HB; apply HM
    apply Hlc; apply Hstore
  | reflect P E _ _ HP HE Hlc =>
    cases HP
    case hole =>
      constructor
      rw [ctx_swap B, ctx_comp B E]
      apply step_lvl.reflect
      apply ctxâ„™.hole; apply ctxğ”¼.consğ”¹
      apply HB; apply HE; apply Hlc
    case consâ„š HQ =>
      constructor
      rw [ctx_comp B P]
      apply step_lvl.reflect
      apply ctxâ„™.consâ„š; apply ctxâ„š.consğ”¹
      apply HB; apply HQ; apply HE; apply Hlc

theorem stepâ„ : âˆ€ intro lvl R stâ‚€ stâ‚ eâ‚€ eâ‚, ctxâ„ intro lvl R â†’ step_lvl (lvl + intro) (stâ‚€, eâ‚€) (stâ‚, eâ‚) â†’ step_lvl lvl (stâ‚€, RâŸ¦eâ‚€âŸ§) (stâ‚, RâŸ¦eâ‚âŸ§) :=
  by
  intros intro lvl R stâ‚€ stâ‚ eâ‚€ eâ‚ HR Hstep
  cases Hstep with
  | stepğ•„ M _ _ _ HM Hlc Hhead =>
    repeat rw [ctx_comp R M]
    apply step_lvl.stepğ•„
    apply ctxğ•„.consâ„; apply HR; apply HM
    apply Hlc; apply Hhead
  | storeğ•„ M _ _ _ _ HM Hlc Hstore =>
    rw [ctx_comp R M]
    apply step_lvl.storeğ•„
    apply ctxğ•„.consâ„; apply HR; apply HM
    apply Hlc; apply Hstore
  | reflect P _ _ _ HP HE Hlc =>
    cases HP
    case hole =>
      apply step_lvl.reflect
      apply ctxâ„™.consâ„š; apply ctxâ„š.holeâ„
      apply HR; apply HE; apply Hlc
    case consâ„š HQ =>
      rw [ctx_comp R P]
      apply step_lvl.reflect
      apply ctxâ„™.consâ„š; apply ctxâ„š.consâ„
      apply HR; apply HQ; apply HE; apply Hlc

theorem fv_headğ•„ : âˆ€ eâ‚€ eâ‚, headğ•„ eâ‚€ eâ‚ â†’ fv eâ‚ âŠ† fv eâ‚€ :=
  by
  intros eâ‚€ eâ‚ Hhead
  cases Hhead <;> simp
  case lets =>
    apply fv_opening
  case appâ‚ =>
    rw [Set.union_comm]
    apply fv_opening
  case lift_lam =>
    rw [â† fv_mapingğ•”]
  case fixâ‚ e =>
    have HEq : fv (.fixâ‚ (.lam e)) âˆª fv e = fv e := by simp
    rw [â† HEq]
    apply fv_opening
