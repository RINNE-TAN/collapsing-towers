
import CollapsingTowers.TwoLevelBasic.Typing
@[simp]
def expr.erase : Expr â†’ Expr
  | .bvar i => .bvar i
  | .fvar y => .fvar y
  | .lam e => .lam (erase e)
  | .lift e => erase e
  | .appâ‚ f arg => .appâ‚ (erase f) (erase arg)
  | .appâ‚‚ f arg => .appâ‚ (erase f) (erase arg)
  | .lit n => .lit n
  | .run e => erase e
  | .code e => erase e
  | .reflect e => erase e
  | .lamğ•” e => .lam (erase e)
  | .lets b e => .lets (erase b) (erase e)
  | .letğ•” b e => .lets (erase b) (erase e)

notation:max "â€–" e "â€–" => expr.erase e

@[simp]
def ty.erase : Ty â†’ Ty
  | .nat => .nat
  | .arrow Ï„a Ï„b _ => .arrow (erase Ï„a) (erase Ï„b) âˆ…
  | .fragment Ï„ => erase Ï„
  | .rep Ï„ => erase Ï„

notation:max "â€–" Ï„ "â€–ğœ" => ty.erase Ï„

@[simp]
def env.erase : TEnv â†’ TEnv
  | [] => []
  | (Ï„, _) :: Î“ => (â€–Ï„â€–ğœ, .stat) :: erase Î“

notation:max "â€–" Î“ "â€–ğ›¾" => env.erase Î“

theorem erase_lc_at : âˆ€ e i, lc_at e i â†” lc_at â€–eâ€– i :=
  by
  intros e i
  induction e generalizing i with
  | fvar| lit| bvar => simp
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letğ•” _ _ IHâ‚€ IHâ‚ =>
    apply and_congr
    apply IHâ‚€; apply IHâ‚
  | code _ IH
  | reflect _ IH
  | lift _ IH
  | run _ IH
  | lam _ IH
  | lamğ•” _ IH =>
    apply IH

theorem erase_closed_at : âˆ€ e x, closed_at e x â†” closed_at â€–eâ€– x :=
  by
  intros e x
  induction e with
  | fvar| lit| bvar => simp
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letğ•” _ _ IHâ‚€ IHâ‚ =>
    apply and_congr
    apply IHâ‚€; apply IHâ‚
  | code _ IH
  | reflect _ IH
  | lift _ IH
  | run _ IH
  | lam _ IH
  | lamğ•” _ IH =>
    apply IH

theorem erase_opening_comm : âˆ€ i v e, â€–(opening i v e)â€– = opening i â€–vâ€– â€–eâ€– :=
  by
  intros i v e
  induction e generalizing i with
  | bvar j =>
    by_cases HEq : j = i
    . simp [if_pos HEq]
    . simp [if_neg HEq]
  | fvar| lit => simp
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letğ•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply IHâ‚
  | code _ IH
  | reflect _ IH
  | lift _ IH
  | run _ IH
  | lam _ IH
  | lamğ•” _ IH => simp; apply IH

theorem erase_openâ‚€_comm : âˆ€ x e, â€–openâ‚€ x eâ€– = openâ‚€ x â€–eâ€– :=
  by
  intros x e; apply erase_opening_comm

theorem erase_open_subst_comm : âˆ€ v e, â€–open_subst v eâ€– = open_subst â€–vâ€– â€–eâ€– :=
  by
  intros v e; apply erase_opening_comm

theorem erase_mapingğ•” : âˆ€ i e, â€–mapingğ•” e iâ€– = â€–eâ€– :=
  by
  intros i e
  induction e generalizing i with
  | bvar j => by_cases HEq : j = i; rw [HEq]; simp; simp [if_neg HEq]
  | fvar y => simp
  | lam _ IH
  | lift _ IH
  | code _ IH
  | reflect _ IH
  | lamğ•” _ IH
  | run _ IH =>
    simp; apply IH
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letğ•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply IHâ‚
  | lit => simp

theorem erase_ty_well_binding_time : âˆ€ ğ•Š Ï„, well_binding_time ğ•Š â€–Ï„â€–ğœ :=
  by
  intros ğ•Š Ï„
  induction Ï„
  case nat => cases ğ•Š <;> simp
  case arrow IHâ‚€ IHâ‚ =>
    cases ğ•Š
    case stat =>
      constructor; apply IHâ‚€; apply IHâ‚
    case dyn =>
      constructor; rfl
      constructor; apply IHâ‚€; apply IHâ‚
  case fragment IH => apply IH
  case rep IH => apply IH

theorem length_erase_env : âˆ€ Î“, Î“.length = â€–Î“â€–ğ›¾.length :=
  by
  intros Î“
  induction Î“
  case nil => rfl
  case cons IH => simp; apply IH

theorem binds_erase_env : âˆ€ x Ï„ ğ•Š Î“, binds x (Ï„, ğ•Š) Î“ â†’ binds x (â€–Ï„â€–ğœ, .stat) â€–Î“â€–ğ›¾ :=
  by
  intros x Ï„ ğ•Š Î“ Hbinds
  induction Î“
  case nil => nomatch Hbinds
  case cons tails IH =>
    by_cases HEq : tails.length = x
    . simp [if_pos HEq] at Hbinds
      simp [â† length_erase_env, if_pos HEq, Hbinds]
    . simp [if_neg HEq] at Hbinds
      simp [â† length_erase_env, if_neg HEq]
      apply IH; apply Hbinds

theorem double_erase : âˆ€ e, â€–â€–eâ€–â€– = â€–eâ€– :=
  by
  intros e
  induction e with
  | bvar j => rfl
  | fvar y => rfl
  | lam _ IH
  | lift _ IH
  | code _ IH
  | reflect _ IH
  | lamğ•” _ IH
  | run _ IH =>
    simp; apply IH
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letğ•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply IHâ‚
  | lit => rfl

theorem double_erase_ty : âˆ€ Ï„, â€–â€–Ï„â€–ğœâ€–ğœ = â€–Ï„â€–ğœ :=
  by
  intros Ï„
  induction Ï„
  case nat => simp
  case arrow IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply IHâ‚
  case fragment IH => apply IH
  case rep IH => apply IH

theorem double_erase_env : âˆ€ Î“, â€–â€–Î“â€–ğ›¾â€–ğ›¾ = â€–Î“â€–ğ›¾ :=
  by
  intros Î“
  induction Î“
  case nil => simp
  case cons IH =>
    simp; constructor
    apply double_erase_ty; apply IH

theorem erase_ctxğ”¹_map :
  âˆ€ B e,
    ctxğ”¹ B â†’
    â€–BâŸ¦eâŸ§â€– = â€–BâŸ¦â€–eâ€–âŸ§â€– :=
  by
  intros B e HB
  cases HB <;> simp [double_erase]

theorem erase_ctxğ”¼_map :
  âˆ€ E e,
    ctxğ”¼ E â†’
    â€–EâŸ¦eâŸ§â€– = â€–EâŸ¦â€–eâ€–âŸ§â€– :=
  by
  intros E e HE
  induction HE generalizing e
  case hole => simp [double_erase]
  case consğ”¹ B E HB HE IH =>
    simp; rw [erase_ctxğ”¹_map _ _ HB, IH, â† erase_ctxğ”¹_map _ _ HB]

-- Î“ âŠ¢ e : Ï„
-- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
-- â€–Î“â€– âŠ¢ â€–eâ€– : â€–Ï„â€–
theorem erase_safety : âˆ€ Î“ ğ•Š e Ï„ Ï†, typing Î“ ğ•Š e Ï„ Ï† â†’ typing â€–Î“â€–ğ›¾ .stat â€–eâ€– â€–Ï„â€–ğœ âˆ… :=
  by
  intros Î“ ğ•Š e Ï„ Ï† HÏ„
  apply
    @typing.rec
      (fun Î“ ğ•Š e Ï„ Ï† (H : typing Î“ ğ•Š e Ï„ Ï†) => typing â€–Î“â€–ğ›¾ .stat â€–eâ€– â€–Ï„â€–ğœ âˆ…)
      (fun Î“ e Ï„ Ï† (H : typing_reification Î“ e Ï„ Ï†) => typing â€–Î“â€–ğ›¾ .stat â€–eâ€– â€–Ï„â€–ğœ âˆ…)
  case fvar =>
    intros _ _ _ _ Hbinds _
    apply typing.fvar
    apply binds_erase_env; apply Hbinds
    apply erase_ty_well_binding_time
  case lam =>
    intros _ _ _ _ _ _ _ HwellBinds Hclose IH
    apply typing.lam
    rw [â† length_erase_env, â† erase_openâ‚€_comm]
    apply IH
    apply erase_ty_well_binding_time
    rw [â† length_erase_env, â† erase_closed_at]
    apply Hclose
  case lift_lam =>
    intros _ _ _ _ _ _ _ IH
    apply IH
  case appâ‚ =>
    intros _ _ _ _ _ _ _ _ _ _ _ IHf IHarg
    rw [â† union_pure_left âˆ…, â† union_pure_left âˆ…]
    apply typing.appâ‚
    apply IHf; apply IHarg
  case appâ‚‚ =>
    intros _ _ _ _ _ _ _ _ _ IHf IHarg
    rw [â† union_pure_left âˆ…, â† union_pure_left âˆ…]
    apply typing.appâ‚
    apply IHf; apply IHarg
  case lit =>
    intros _ _ _
    apply typing.lit
  case lift_lit =>
    intros _ _ _ _ IH
    apply IH
  case code_fragment =>
    intros _ x _ Hbinds HwellBinds
    apply typing.fvar
    simp; apply binds_erase_env; apply Hbinds
    apply erase_ty_well_binding_time
  case code_rep =>
    intros _ _ _ _ IH
    apply IH
  case reflect =>
    intros _ _ _ _ IH
    apply IH
  case lamğ•” =>
    intros _ _ _ _ _ _ _ Hclose IH
    apply typing.lam
    rw [â† length_erase_env, â† erase_openâ‚€_comm]
    apply IH
    apply erase_ty_well_binding_time
    rw [â† length_erase_env, â† erase_closed_at]
    apply Hclose
  case lets =>
    intros _ _ _ _ _ _ _ _ _ _ _ Hclose IHb IHe
    rw [â† union_pure_left âˆ…]
    apply typing.lets
    apply IHb
    rw [â† length_erase_env, â† erase_openâ‚€_comm]
    apply IHe
    apply erase_ty_well_binding_time
    rw [â† length_erase_env, â† erase_closed_at]
    apply Hclose
  case letğ•” =>
    intros _ _ _ _ _ _ _ _ HwellBinds Hclose IHb IHe
    rw [â† union_pure_left âˆ…]
    apply typing.lets
    apply IHb
    rw [â† length_erase_env, â† erase_openâ‚€_comm]
    apply IHe
    apply erase_ty_well_binding_time
    rw [â† length_erase_env, â† erase_closed_at]
    apply Hclose
  case run =>
    intros _ _ _ _ _ _ IH
    apply IH
  case pure =>
    intros _ _ _ _ IH
    apply IH
  case reify =>
    intros _ _ _ _ _ IH
    apply IH
  apply HÏ„

theorem erase_reification_safety : âˆ€ Î“ e Ï„ Ï†, typing_reification Î“ e Ï„ Ï† â†’ typing_reification â€–Î“â€–ğ›¾ â€–eâ€– â€–Ï„â€–ğœ âˆ… :=
  by
  intros Î“ e Ï„ Ï† HÏ„
  cases HÏ„ <;>
  next HÏ„ =>
    apply typing_reification.pure
    apply erase_safety _ _ _ _ _ HÏ„

theorem erase_typing_reification_iff_typing :
  âˆ€ Î“ e Ï„,
    typing â€–Î“â€–ğ›¾ .stat â€–eâ€– â€–Ï„â€–ğœ âˆ… â†” typing_reification â€–Î“â€–ğ›¾ â€–eâ€– â€–Ï„â€–ğœ âˆ… :=
  by
  intros Î“ e Ï„
  constructor
  . apply typing_reification.pure
  . generalize HEq : â€–Ï„â€–ğœ = Ï„ğ•–
    intros HÏ„; cases HÏ„
    case pure HÏ„ => apply HÏ„
    case reify =>
      exfalso
      induction Ï„ <;> simp at HEq
      case fragment IH =>
        apply IH; apply HEq
      case rep IH =>
        apply IH; apply HEq
