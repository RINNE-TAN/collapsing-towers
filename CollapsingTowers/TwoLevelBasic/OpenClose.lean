
import Mathlib.Data.Set.Insert
import CollapsingTowers.TwoLevelBasic.Syntax
-- Definitions
@[simp]
def subst (x : â„•) (v : Expr) : Expr â†’ Expr
  | .bvar i => .bvar i
  | .fvar y => if x = y then v else .fvar y
  | .lam e => .lam (subst x v e)
  | .lift e => .lift (subst x v e)
  | .appâ‚ f arg => .appâ‚ (subst x v f) (subst x v arg)
  | .appâ‚‚ f arg => .appâ‚‚ (subst x v f) (subst x v arg)
  | .lit n => .lit n
  | .run e => .run (subst x v e)
  | .code e => .code (subst x v e)
  | .reflect e => .reflect (subst x v e)
  | .lamð•” e => .lamð•” (subst x v e)
  | .lets b e => .lets (subst x v b) (subst x v e)
  | .letð•” b e => .letð•” (subst x v b) (subst x v e)

-- opening i t1 t2 = [i â†’ t1]t2
@[simp]
def opening (i : â„•) (x : Expr) : Expr â†’ Expr
  | .bvar j => if j = i then x else .bvar j
  | .fvar x => .fvar x
  | .lam e => .lam (opening (i + 1) x e)
  | .lift e => .lift (opening i x e)
  | .appâ‚ f arg => .appâ‚ (opening i x f) (opening i x arg)
  | .appâ‚‚ f arg => .appâ‚‚ (opening i x f) (opening i x arg)
  | .lit n => .lit n
  | .run e => .run (opening i x e)
  | .code e => .code (opening i x e)
  | .reflect e => .reflect (opening i x e)
  | .lamð•” e => .lamð•” (opening (i + 1) x e)
  | .lets b e => .lets (opening i x b) (opening (i + 1) x e)
  | .letð•” b e => .letð•” (opening i x b) (opening (i + 1) x e)

@[simp]
def openâ‚€ (x : â„•) : Expr â†’ Expr :=
  opening 0 (.fvar x)

@[simp]
def open_subst (tgt : Expr) (within : Expr) :=
  opening 0 tgt within

@[simp]
def closing (i : â„•) (x : â„•) : Expr â†’ Expr
  | .bvar j => .bvar j
  | .fvar y => if x == y then .bvar i else .fvar y
  | .lam e => .lam (closing (i + 1) x e)
  | .lift e => .lift (closing i x e)
  | .appâ‚ f arg => .appâ‚ (closing i x f) (closing i x arg)
  | .appâ‚‚ f arg => .appâ‚‚ (closing i x f) (closing i x arg)
  | .lit n => .lit n
  | .run e => .run (closing i x e)
  | .code e => .code (closing i x e)
  | .reflect e => .reflect (closing i x e)
  | .lamð•” e => .lamð•” (closing (i + 1) x e)
  | .lets b e => .lets (closing i x b) (closing (i + 1) x e)
  | .letð•” b e => .letð•” (closing i x b) (closing (i + 1) x e)

@[simp]
def closeâ‚€ : â„• â†’ Expr â†’ Expr :=
  closing 0

-- closedness condition for free variables
@[simp]
def closed_at (e : Expr) (f : â„•) : Prop :=
  match e with
  | .bvar _ => true
  | .fvar x => x < f
  | .lam e => closed_at e f
  | .lift e => closed_at e f
  | .appâ‚ e1 e2 => closed_at e1 f âˆ§ closed_at e2 f
  | .appâ‚‚ e1 e2 => closed_at e1 f âˆ§ closed_at e2 f
  | .lit _ => true
  | .run e => closed_at e f
  | .code e => closed_at e f
  | .reflect e => closed_at e f
  | .lamð•” e => closed_at e f
  | .lets b e => closed_at b f âˆ§ closed_at e f
  | .letð•” b e => closed_at b f âˆ§ closed_at e f

@[simp]
def closed e := closed_at e 0

-- closedness condition for bound variables
@[simp]
def lc_at (e : Expr) (b : â„•) : Prop :=
  match e with
  | .bvar x => x < b
  | .fvar _ => true
  | .lam e => lc_at e (b + 1)
  | .lift e => lc_at e b
  | .appâ‚ e1 e2 => lc_at e1 b âˆ§ lc_at e2 b
  | .appâ‚‚ e1 e2 => lc_at e1 b âˆ§ lc_at e2 b
  | .lit _ => true
  | .run e => lc_at e b
  | .code e => lc_at e b
  | .reflect e => lc_at e b
  | .lamð•” e => lc_at e (b + 1)
  | .lets e1 e2 => lc_at e1 b âˆ§ lc_at e2 (b + 1)
  | .letð•” e1 e2 => lc_at e1 b âˆ§ lc_at e2 (b + 1)

@[simp]
def lc e := lc_at e 0

@[simp]
def mapingð•” (e : Expr) (i : â„•) : Expr :=
  match e with
  | .bvar j => if j == i then (.code (.bvar i)) else .bvar j
  | .fvar x => .fvar x
  | .lam e => .lam (mapingð•” e (i + 1))
  | .lift e => .lift (mapingð•” e i)
  | .appâ‚ f arg => .appâ‚ (mapingð•” f i) (mapingð•” arg i)
  | .appâ‚‚ f arg => .appâ‚‚ (mapingð•” f i) (mapingð•” arg i)
  | .lit n => .lit n
  | .run e => .run (mapingð•” e i)
  | .code e => .code (mapingð•” e i)
  | .reflect e => .reflect (mapingð•” e i)
  | .lamð•” e => .lamð•” (mapingð•” e (i + 1))
  | .lets b e => .lets (mapingð•” b i) (mapingð•” e (i + 1))
  | .letð•” b e => .letð•” (mapingð•” b i) (mapingð•” e (i + 1))

@[simp]
def mapð•”â‚€ (e : Expr) : Expr := mapingð•” e 0

@[simp]
def fv : Expr â†’ Set â„•
  | .bvar _ => âˆ…
  | .fvar x => { x }
  | .lam e => fv e
  | .lift e => fv e
  | .appâ‚ f arg => fv f âˆª fv arg
  | .appâ‚‚ f arg => fv f âˆª fv arg
  | .lit _ => âˆ…
  | .run e => fv e
  | .code e => fv e
  | .reflect e => fv e
  | .lamð•” e => fv e
  | .lets b e => fv b âˆª fv e
  | .letð•” b e => fv b âˆª fv e

@[simp]
def wf (e : Expr) : Prop := lc e âˆ§ closed e

abbrev Subst :=
  List Expr

@[simp]
def multi_subst : Subst â†’ Expr â†’ Expr
  | [], e => e
  | v :: Î³, e => multi_subst Î³ (subst Î³.length v e)

@[simp]
def multi_wf : Subst â†’ Prop
  | [] => true
  | v :: Î³ => wf v âˆ§ multi_wf Î³

-- Properties
lemma subst_intro : âˆ€ x e v i, closed_at e x â†’ subst x v (opening i (.fvar x) e) = opening i v e :=
  by
  intros _ e _ i Hclosed
  induction e generalizing i with
  | bvar j =>
    by_cases HEq : j = i
    . simp [HEq]
    . simp [if_neg HEq]
  | fvar y =>
    simp at *; omega
  | lam _ IH
  | lift _ IH
  | code _ IH
  | reflect _ IH
  | lamð•” _ IH
  | run _ IH =>
    simp; apply IH; apply Hclosed
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply Hclosed.left
    apply IHâ‚; apply Hclosed.right
  | lit => simp

lemma subst_closed_id : âˆ€ x e v, closed_at e x â†’ subst x v e = e :=
  by
  intros x e v He
  induction e with
  | bvar => simp
  | fvar => simp at *; omega
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    simp; apply IH; apply He
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply He.left
    apply IHâ‚; apply He.right
  | lets _ _ IHb IH
  | letð•” _ _ IHb IH =>
    simp; constructor
    apply IHb; apply He.left
    apply IH; apply He.right
  | lit => simp

lemma openSubst_intro : âˆ€ x e v, closed_at e x â†’ subst x v (openâ‚€ x e) = open_subst v e :=
  by
  intros _ _ _ Hclosed
  apply subst_intro
  apply Hclosed

lemma lc_inc: âˆ€ t i j,
    lc_at t i â†’ i â‰¤ j â†’
    lc_at t j := by
  intros t i j Hclose HLe
  induction t generalizing i j with
  | bvar => simp at *; omega
  | fvar => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH; apply Hclose; omega
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    apply And.intro
    . apply IHâ‚€; apply Hclose.left; omega
    . apply IHâ‚; apply Hclose.right; omega
  | lit => simp

lemma closed_inc : âˆ€ x y e, closed_at e x â†’ x â‰¤ y â†’ closed_at e y :=
  by
  intros x y e Hclose Hxy
  induction e with
  | bvar j => simp
  | fvar z => simp at *; omega
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply Hclose.left
    apply IHâ‚; apply Hclose.right
  | lit => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    simp; apply IH; apply Hclose

lemma subst_lc_at : âˆ€ x e v i, lc_at v i â†’ lc_at e i â†’ lc_at (subst x v e) i :=
  by
  intros x e v i Hv He
  induction e generalizing i with
  | bvar => apply He
  | fvar y =>
    by_cases HEq : x = y
    . rw [HEq]; simp; apply Hv
    . simp; rw [if_neg HEq]; simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH =>
    apply IH; apply lc_inc; apply Hv; omega; apply He
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚ =>
    constructor
    apply IHâ‚€; apply Hv; apply He.left
    apply IHâ‚; apply Hv; apply He.right
  | lets _ _ IHb IH
  | letð•” _ _ IHb IH =>
    constructor
    apply IHb; apply Hv; apply He.left
    apply IH; apply lc_inc; apply Hv; omega; apply He.right
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    simp; apply IH; apply Hv; apply He
  | lit => simp

lemma subst_closed_at : âˆ€ x e v y, closed_at v y â†’ closed_at e y â†’ closed_at (subst x v e) y :=
  by
  intros x e v y Hv He
  induction e generalizing y with
  | bvar => apply He
  | fvar z =>
    by_cases HEq : x = z
    . rw [HEq]; simp; apply Hv
    . simp; rw [if_neg HEq]; apply He
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH =>
    apply IH; apply Hv; apply He
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    constructor
    apply IHâ‚€; apply Hv; apply He.left
    apply IHâ‚; apply Hv; apply He.right
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    simp; apply IH; apply Hv; apply He
  | lit => simp

lemma subst_closed_at_dec : âˆ€ x e v, closed_at v x â†’ closed_at e (x + 1) â†’ closed_at (subst x v e) x :=
  by
  intros x e v Hv He
  induction e with
  | bvar => apply He
  | fvar z =>
    by_cases HEq : x = z
    . rw [â† HEq]; simp; apply Hv
    . simp; rw [if_neg HEq]; simp at *; omega
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH =>
    apply IH; apply He
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    constructor
    apply IHâ‚€; apply He.left
    apply IHâ‚; apply He.right
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    simp; apply IH; apply He
  | lit => simp

lemma open_lc : âˆ€ i x e, lc_at (opening i (.fvar x) e) i â†” lc_at e (i + 1) :=
  by
  intros i x e
  induction e generalizing i with
  | bvar j =>
    simp; by_cases HEq : j = i
    . rw [if_pos HEq]; constructor
      . omega
      . simp
    . rw [if_neg HEq]; simp; omega
  | fvar => simp
  | lit => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    constructor
    . intro Hlc
      constructor
      apply (IHâ‚€ _).mp; apply Hlc.left
      apply (IHâ‚ _).mp; apply Hlc.right
    . intro Hlc
      constructor
      apply (IHâ‚€ _).mpr; apply Hlc.left
      apply (IHâ‚ _).mpr; apply Hlc.right

lemma close_closed : âˆ€ e x i, closed_at e (x + 1) â†” closed_at (closing i x e) x :=
  by
  intros e x i
  induction e generalizing i with
  | fvar y =>
    by_cases HEq : x = y
    . rw [HEq]; simp
    . simp; rw [if_neg HEq]; simp; omega
  | bvar => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    constructor
    . intro Hclose; constructor
      apply (IHâ‚€ _).mp; apply Hclose.left
      apply (IHâ‚ _).mp; apply Hclose.right
    . intro Hclose; constructor
      apply (IHâ‚€ _).mpr; apply Hclose.left
      apply (IHâ‚ _).mpr; apply Hclose.right
  | lit => simp

lemma open_subst_closed : âˆ€ x e v i, closed_at e x â†’ closed_at v x â†’ closed_at (opening i v e) x :=
  by
  intros x e v i He Hv
  induction e generalizing i with
  | bvar j =>
    simp; by_cases HEq : j = i
    . rw [if_pos HEq]; apply Hv
    . rw [if_neg HEq]; simp
  | fvar => apply He
  | lit => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH; apply He
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    constructor
    apply IHâ‚€; apply He.left
    apply IHâ‚; apply He.right

lemma open_closed : âˆ€ e x i, closed_at e x â†’ closed_at (opening i (.fvar x) e) (x + 1) :=
  by
  intros e x i
  induction e generalizing i with
  | fvar y => simp; omega
  | bvar j =>
    by_cases HEq : j = i
    . rw [HEq]; simp
    . simp; rw [if_neg HEq]; simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    intro Hclose; constructor
    apply IHâ‚€; apply Hclose.left
    apply IHâ‚; apply Hclose.right
  | lit => simp

lemma close_lc : âˆ€ e x i j, j < i â†’ lc_at e i â†’ lc_at (closing j x e) i :=
  by
  intros e x i j Hlt
  induction e generalizing i j with
  | fvar y =>
    by_cases HEq : x = y
    . rw [HEq]; simp; omega
    . simp; rw [if_neg HEq]; simp
  | bvar => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH; omega
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    intro Hclose; constructor
    apply IHâ‚€; omega; apply Hclose.left
    apply IHâ‚; omega; apply Hclose.right
  | lit => simp

lemma lc_opening_id : âˆ€ e v i, lc_at e i â†’ opening i v e = e :=
  by
  intros e v i Hlc
  induction e generalizing i with
  | fvar y => simp
  | bvar j => simp at *; omega
  | lift _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH
  | lam _ IH
  | lamð•” _ IH =>
    simp; apply IH; apply Hlc
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply Hlc.left
    apply IHâ‚; apply Hlc.right
  | lit => simp

lemma open_close_id : âˆ€ i e x, lc_at e i â†’ opening i (.fvar x) (closing i x e) = e :=
  by
  intros i e x Hlc
  induction e generalizing i with
  | bvar j =>
    simp
    intro HEq
    rw [HEq] at Hlc
    simp at Hlc
  | fvar y =>
    simp
    by_cases HEq : x = y
    . rw [HEq]; simp
    . rw [if_neg HEq]; simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    simp; apply IH; apply Hlc
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply Hlc.left
    apply IHâ‚; apply Hlc.right
  | lit => rfl

lemma open_close_idâ‚€ : âˆ€ e x, lc e â†’ openâ‚€ x (closeâ‚€ x e) = e := by apply open_close_id

lemma close_open_id : âˆ€ i e x, closed_at e x â†’ closing i x (opening i (.fvar x) e) = e :=
  by
  intros i e x Hclose
  induction e generalizing i with
  | bvar j =>
    by_cases HEq : j = i
    . simp; rw [if_pos HEq]; simp; omega
    . simp; rw [if_neg HEq]; simp
  | fvar y => simp at *; omega
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    simp; apply IH; apply Hclose
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply Hclose.left
    apply IHâ‚; apply Hclose.right
  | lit => rfl

lemma close_open_idâ‚€ : âˆ€ e x, closed_at e x â†’ closeâ‚€ x (openâ‚€ x e) = e := by apply close_open_id

lemma subst_opening_comm :
    âˆ€ x vâ‚€ vâ‚ e i, lc_at vâ‚€ i â†’ subst x vâ‚€ (opening i vâ‚ e) = opening i (subst x vâ‚€ vâ‚) (subst x vâ‚€ e) :=
  by
  intro x vâ‚€ vâ‚ e i Hlc
  induction e generalizing i with
  | bvar j =>
    by_cases HEq : j = i
    . simp; rw [if_pos HEq]; simp; omega
    . simp; rw [if_neg HEq, if_neg HEq]; simp
  | fvar z =>
    by_cases HEq : x = z
    . simp; rw [if_pos HEq]; rw [lc_opening_id]; apply Hlc
    . simp; rw [if_neg HEq]; simp
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply Hlc
    apply IHâ‚; apply Hlc
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply Hlc
    apply IHâ‚; apply lc_inc; apply Hlc; omega
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    simp; apply IH; apply Hlc
  | lit => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH =>
    simp; apply IH; apply lc_inc; apply Hlc; omega

lemma subst_openâ‚€_comm : âˆ€ x y e v, x â‰  y â†’ lc v â†’ subst x v (openâ‚€ y e) = openâ‚€ y (subst x v e) := by
  intros x y e v HNe Hlc
  rw [openâ‚€]; rw [subst_opening_comm]
  simp [if_neg HNe]; apply Hlc

lemma subst_comm : âˆ€ x y vâ‚€ vâ‚ e, x â‰  y â†’ closed vâ‚€ â†’ closed vâ‚ â†’ subst x vâ‚€ (subst y vâ‚ e) = subst y vâ‚ (subst x vâ‚€ e) :=
  by
  intro x y vâ‚€ vâ‚ e HNe Hcloseâ‚€ Hcloseâ‚
  induction e with
  | bvar j => simp
  | fvar z =>
    by_cases HEqx : x = z
    . simp [if_pos HEqx]
      by_cases HEqy : y = z
      . simp [if_pos HEqy]
        omega
      . simp [if_neg HEqy, if_pos HEqx]
        rw [subst_closed_id]
        apply closed_inc; apply Hcloseâ‚€; omega
    . simp [if_neg HEqx]
      by_cases HEqy : y = z
      . simp [if_pos HEqy]
        rw [subst_closed_id]
        apply closed_inc; apply Hcloseâ‚; omega
      . simp [if_neg HEqy, if_neg HEqx]
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply IHâ‚€; apply IHâ‚
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    simp; apply IH
  | lit => simp

example : mapð•”â‚€ (.appâ‚ (.bvar 0) (.lam (.bvar 1))) = .appâ‚ (.code (.bvar 0)) (.lam (.code (.bvar 1))) := by simp

lemma mapingð•”_intro :
    âˆ€ x e i, closed_at e x â†’ closing i x (subst x (.code (.fvar x)) (opening i (.fvar x) e)) = mapingð•” e i :=
  by
  intros x e i Hclose
  induction e generalizing i with
  | bvar j => by_cases HEq : j = i; rw [HEq]; simp; simp [if_neg HEq]
  | fvar y => simp at *; by_cases HEq : x = y; omega; rw [if_neg HEq]; simp; apply HEq
  | lam _ IH
  | lift _ IH
  | code _ IH
  | reflect _ IH
  | lamð•” _ IH
  | run _ IH =>
    simp at *; apply IH; apply Hclose
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp at *; constructor
    apply IHâ‚€; apply Hclose.left
    apply IHâ‚; apply Hclose.right
  | lit => simp

lemma mapð•”â‚€_intro : âˆ€ x e, closed_at e x â†’ closeâ‚€ x (subst x (.code (.fvar x)) (openâ‚€ x e)) = mapð•”â‚€ e :=
  by
  intro _ _ Hclose
  apply mapingð•”_intro
  apply Hclose

lemma mapingð•”_closed : âˆ€ x e i, closed_at e x â†’ closed_at (mapingð•” e i) x :=
  by
  intros x e i He
  induction e generalizing i with
  | bvar j =>
    simp; by_cases HEq : j = i
    . rw [if_pos HEq]; apply He
    . rw [if_neg HEq]; simp
  | fvar => apply He
  | lit => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH; apply He
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    constructor
    apply IHâ‚€; apply He.left
    apply IHâ‚; apply He.right

lemma fv_if_closed_at :
  âˆ€ x y e,
    closed_at e x â†’
    y â‰¥ x â†’
    y âˆ‰ fv e :=
  by
  intros x y e Hclose HGe HIn
  induction e with
  | bvar => nomatch HIn
  | fvar z =>
    simp at *
    omega
  | lit => nomatch HIn
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH; apply Hclose; apply HIn
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    cases HIn
    case inl Hâ‚€ =>
      apply IHâ‚€; apply Hclose.left; apply Hâ‚€
    case inr Hâ‚ =>
      apply IHâ‚; apply Hclose.right; apply Hâ‚

lemma fv_opening : âˆ€ i v e, fv (opening i v e) âŠ† fv v âˆª fv e :=
  by
  intros i v e
  induction e generalizing i with
  | bvar j =>
    simp; by_cases HEq : j = i
    . rw [if_pos HEq]
    . rw [if_neg HEq]; simp
  | fvar z => simp
  | lit => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; constructor
    apply Set.Subset.trans; apply IHâ‚€
    apply Set.union_subset_union; rfl; simp
    apply Set.Subset.trans; apply IHâ‚
    apply Set.union_subset_union; rfl; simp

lemma fv_openâ‚€ :
  âˆ€ x y e,
    x âˆ‰ fv e â†’
    x â‰  y â†’
    x âˆ‰ fv (openâ‚€ y e) :=
  by
  intros x y e HNotIn HNe HIn
  apply HNotIn
  have H : fv (openâ‚€ y e) âŠ† { y } âˆª fv e := by apply fv_opening
  rw [Set.subset_def] at H
  cases (H x HIn)
  case inl => simp at *; omega
  case inr => assumption

lemma fv_closed_at_dec :
  âˆ€ e x,
    closed_at e (x + 1) â†’
    x âˆ‰ fv e â†’
    closed_at e x :=
  by
  intros e x Hclose HFv
  induction e with
  | bvar j => simp
  | fvar y => simp at *; omega
  | lit => simp
  | lift _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH
  | lam _ IH
  | lamð•” _ IH =>
    apply IH; apply Hclose; apply HFv
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp at HFv; constructor
    apply IHâ‚€; apply Hclose.left; apply HFv.left
    apply IHâ‚; apply Hclose.right; apply HFv.right

lemma fv_mapingð•” : âˆ€ e i, fv e = fv (mapingð•” e i) :=
  by
  intros e i
  induction e generalizing i with
  | bvar j =>
    simp; by_cases HEq : j = i
    . rw [if_pos HEq]; rfl
    . rw [if_neg HEq]; rfl
  | fvar => rfl
  | lit => rfl
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH => apply IH
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; rw [IHâ‚€, IHâ‚]

lemma fv_empty_iff_closed : âˆ€ e, fv e = âˆ… â†” closed e :=
  by
  intro e
  induction e with
  | bvar => simp
  | fvar => simp
  | lit => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    constructor
    . intro HFv; simp at HFv
      constructor
      apply IHâ‚€.mp; apply HFv.left
      apply IHâ‚.mp; apply HFv.right
    . intro Hclose
      simp; constructor
      apply IHâ‚€.mpr; apply Hclose.left
      apply IHâ‚.mpr; apply Hclose.right

lemma fv_closing : âˆ€ i x e, fv (closing i x e) = fv e \ { x } :=
  by
  intros i x e
  induction e generalizing i with
  | bvar => simp
  | fvar y =>
    simp; by_cases HEq : x = y
    . rw [if_pos HEq]
      rw [HEq]; simp
    . rw [if_neg HEq]
      rw [Set.diff_singleton_eq_self]
      rfl; apply HEq
  | lit => simp
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp; rw [IHâ‚€, IHâ‚]
    rw [Set.union_diff_distrib]

lemma fv_subset_closed :
  âˆ€ eâ‚€ eâ‚ x,
    fv eâ‚ âŠ† fv eâ‚€ â†’
    closed_at eâ‚€ x â†’
    closed_at eâ‚ x :=
  by
  intros eâ‚€ eâ‚ x HFv Hclose
  induction eâ‚ with
  | bvar| lit => simp
  | fvar y =>
    simp at *
    have _ : Â¬y â‰¥ x := by
      intro HGe
      apply fv_if_closed_at; apply Hclose
      apply HGe; apply HFv
    omega
  | lam _ IH
  | lift _ IH
  | lamð•” _ IH
  | code _ IH
  | reflect _ IH
  | run _ IH =>
    apply IH; apply HFv
  | appâ‚ _ _ IHâ‚€ IHâ‚
  | appâ‚‚ _ _ IHâ‚€ IHâ‚
  | lets _ _ IHâ‚€ IHâ‚
  | letð•” _ _ IHâ‚€ IHâ‚ =>
    simp at HFv; constructor
    apply IHâ‚€; apply HFv.left
    apply IHâ‚; apply HFv.right

@[simp]
lemma multi_subst_fvar: âˆ€ Î³ x, x â‰¥ Î³.length â†’ multi_subst Î³ (.fvar x) = .fvar x :=
  by
  intros Î³ x HGe
  induction Î³
  case nil => rfl
  case cons tail IH =>
    simp at HGe
    by_cases HEq : tail.length = x
    . omega
    . simp [if_neg HEq]
      apply IH; omega

@[simp]
lemma multi_subst_appâ‚ : âˆ€ Î³ f arg, multi_subst Î³ (.appâ‚ f arg) = .appâ‚ (multi_subst Î³ f) (multi_subst Î³ arg) :=
  by
  intros Î³ f arg
  induction Î³ generalizing f arg
  case nil => rfl
  case cons IH => simp [IH]

@[simp]
lemma multi_subst_lam : âˆ€ Î³ e, multi_subst Î³ (.lam e) = .lam (multi_subst Î³ e) :=
  by
  intros Î³ e
  induction Î³ generalizing e
  case nil => rfl
  case cons IH => simp [IH]

@[simp]
lemma multi_subst_lit : âˆ€ Î³ n, multi_subst Î³ (.lit n) = .lit n :=
  by
  intros Î³ n
  induction Î³
  case nil => rfl
  case cons IH => simp [IH]

@[simp]
lemma multi_subst_lets : âˆ€ Î³ b e, multi_subst Î³ (.lets b e) = .lets (multi_subst Î³ b) (multi_subst Î³ e) :=
  by
  intros Î³ b e
  induction Î³ generalizing b e
  case nil => rfl
  case cons IH => simp [IH]

lemma multi_subst_lc_at : âˆ€ i Î³ e, multi_wf Î³ â†’ lc_at e i â†’ lc_at (multi_subst Î³ e) i :=
  by
  intros i Î³ e HÎ³ He
  induction Î³ generalizing e
  case nil => apply He
  case cons IH =>
    apply IH; apply HÎ³.right
    apply subst_lc_at; apply lc_inc
    apply HÎ³.left.left; omega; apply He

lemma multi_subst_comm : âˆ€ x Î³ v e, x â‰¥ Î³.length â†’ closed v â†’  multi_wf Î³ â†’ subst x v (multi_subst Î³ e) = multi_subst Î³ (subst x v e) :=
  by
  intro x Î³ v e HGe Hclose HÎ³
  induction Î³ generalizing e
  case nil => simp
  case cons IH =>
    simp at HGe
    rw [multi_subst, multi_subst, IH, subst_comm]
    omega; apply Hclose; apply HÎ³.left.right
    omega; apply HÎ³.right

lemma multi_subst_closed : âˆ€ Î³ e, multi_wf Î³ â†’ closed_at e Î³.length â†’ closed (multi_subst Î³ e) :=
  by
  intros Î³ e HÎ³ He
  induction Î³ generalizing e
  case nil => apply He
  case cons IH =>
    apply IH; apply HÎ³.right
    apply subst_closed_at_dec; apply closed_inc; apply HÎ³.left.right; omega
    apply He

lemma multi_subst_opening_comm :
    âˆ€ Î³ v e i, multi_wf Î³ â†’ multi_subst Î³ (opening i v e) = opening i (multi_subst Î³ v) (multi_subst Î³ e) :=
    by
    intros Î³ v e i HÎ³
    induction Î³ generalizing e v
    case nil => rfl
    case cons IH =>
      rw [multi_subst, subst_opening_comm, IH]
      rfl; apply HÎ³.right
      apply lc_inc; apply HÎ³.left.left; omega

lemma multi_subst_openâ‚€_comm :
    âˆ€ Î³ e x, x â‰¥ Î³.length â†’ multi_wf Î³ â†’ multi_subst Î³ (opening 0 (.fvar x) e) = opening 0 (.fvar x) (multi_subst Î³ e) :=
  by
  intros Î³ e x HGe HÎ³
  rw [multi_subst_opening_comm, multi_subst_fvar]
  apply HGe; apply HÎ³

lemma multi_subst_open_subst_comm :
    âˆ€ Î³ v e, multi_wf Î³ â†’ multi_subst Î³ (open_subst v e) = open_subst (multi_subst Î³ v) (multi_subst Î³ e) :=
  by
  intros Î³ v e
  apply multi_subst_opening_comm

lemma multi_subst_closed_id : âˆ€ Î³ e, closed e â†’ multi_subst Î³ e = e :=
  by
  intro Î³ e Hclose
  induction Î³ generalizing e
  case nil => rfl
  case cons IH =>
    rw [multi_subst, IH, subst_closed_id]
    apply closed_inc; apply Hclose; omega
    rw [subst_closed_id]; apply Hclose
    apply closed_inc; apply Hclose; omega
