import CollapsingTowers.TwoLevelRec.CtxEquiv.Defs
import CollapsingTowers.TwoLevelRec.LogicalEquiv.Fundamental

-- Î“ âŠ§ eâ‚€ â‰¤ğ‘ğ‘–ğ‘¢ eâ‚ : Ï„ â‰œ
--   Î“ âŠ¢ eâ‚€ : Ï„ âˆ§
--   Î“ âŠ¢ eâ‚ : Ï„ âˆ§
--   âˆ€ (â¦° âŠ¢ Î³ : Î“, â¦° âŠ¢ EâŸ¦â¦° âŠ¢ Ï„âŸ§ : Ï„ğ•–).
--   EâŸ¦Î³(eâ‚€)âŸ§â‡“ â†’ EâŸ¦Î³(eâ‚)âŸ§â‡“
@[simp]
def ciu_approx (Î“ : TEnv) (eâ‚€ eâ‚: Expr) (Ï„ : Ty) : Prop :=
  typing Î“ ğŸš eâ‚€ Ï„ âŠ¥ âˆ§
  typing Î“ ğŸš eâ‚ Ï„ âŠ¥ âˆ§
    âˆ€ Î³, typing.subst Î³ Î“ â†’
    âˆ€ E Ï„ğ•–,
      ctxğ”¼ E â†’
      ObsCtxâ„‚ â¦° Ï„ E â¦° Ï„ğ•– â†’
      termination EâŸ¦msubst Î³ eâ‚€âŸ§ â†’
      termination EâŸ¦msubst Î³ eâ‚âŸ§

-- Î“ âŠ§ eâ‚€ â‰¤ğ‘ğ‘¡ğ‘¥ eâ‚ : Ï„
-- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
-- Î“ âŠ§ eâ‚€ â‰¤ğ‘ğ‘–ğ‘¢ eâ‚ : Ï„
theorem ctx_approx_impl_ciu_approx :
  âˆ€ Î“ Ï„ eâ‚€ eâ‚,
    ctx_approx Î“ eâ‚€ eâ‚ Ï„ â†’
    ciu_approx Î“ eâ‚€ eâ‚ Ï„ :=
  by
  intros Î“ Ï„ eâ‚€ eâ‚ Hctx
  constructor; apply Hctx.left
  constructor; apply Hctx.right.left
  intros Î³ HÎ³
  induction HÎ³ generalizing eâ‚€ eâ‚
  case nil =>
    intros E Ï„ğ•– HE
    apply Hctx.right.right
  case cons v Î³ Ï„ğ•§ Î“ Hvalue HÏ„v HÏ„Î³ IH =>
    intros E Ï„ğ•– HE HCE Hterminationâ‚€
    have HEq := typing.subst.length _ _ HÏ„Î³
    have HsemÎ“ := log_approx_env.refl 0 _ _ HÏ„Î³
    have âŸ¨Hmwfâ‚€, Hmwfâ‚âŸ© := log_approx_env.mwf _ _ _ _ HsemÎ“
    have âŸ¨HÏ„eâ‚€, HÏ„eâ‚, _âŸ© := Hctx
    have HEqSubstâ‚€ : msubst Î³ (subst (List.length Î“) v eâ‚€) = opening 0 (msubst Î³ v) (msubst Î³ {0 â†¤ List.length Î“}eâ‚€) :=
      by
      rw [â† comm.msubst_opening_value, â† intro.subst, identity.opening_closing]
      apply typing.regular _ _ _ _ _ HÏ„eâ‚€
      rw [â† closed.under_closing]; apply typing.closed_at_env _ _ _ _ _ HÏ„eâ‚€
      apply Hmwfâ‚€
    have HEqSubstâ‚ : msubst Î³ (subst (List.length Î“) v eâ‚) = opening 0 (msubst Î³ v) (msubst Î³ {0 â†¤ List.length Î“}eâ‚) :=
      by
      rw [â† comm.msubst_opening_value, â† intro.subst, identity.opening_closing]
      apply typing.regular _ _ _ _ _ HÏ„eâ‚
      rw [â† closed.under_closing]; apply typing.closed_at_env _ _ _ _ _ HÏ„eâ‚
      apply Hmwfâ‚
    --
    --
    -- (x â†¦ Ï„ğ•§, Î“) âŠ§ eâ‚€ â‰¤ğ‘ğ‘¡ğ‘¥ eâ‚ : Ï„
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- Î“ âŠ§ Î»x.eâ‚€ @ v â‰¤ğ‘ğ‘¡ğ‘¥ Î»x.eâ‚ @ v : Ï„
    have Hctx : ctx_approx Î“ (.appâ‚ (.lam {0 â†¤ Î“.length}eâ‚€) v) (.appâ‚ (.lam {0 â†¤ Î“.length}eâ‚) v) Ï„ :=
      by
      apply ctx_approx.congruence_under_ObsCtxâ„‚ _ _ _ _ _ _ _ Hctx
      have âŸ¨Hwbt, _âŸ© := typing.dynamic_impl_pure _ _ _ _ HÏ„v
      have HÏ„v := typing.weakening _ Î“ _ _ _ _ HÏ„v
      simp at HÏ„v
      have HÏ„C := ObsCtxâ„‚.hole Î“ Ï„
      have HÏ„B := ObsCtxğ”¹.applâ‚ Î“ v Ï„ğ•§ Ï„ HÏ„v
      have HÏ„C := ObsCtxâ„‚.consğ”¹ _ _ _ _ _ _ _ _ HÏ„C HÏ„B
      have HÏ„B := ObsCtxğ”¹.lam Î“ Ï„ğ•§ Ï„ Hwbt
      apply ObsCtxâ„‚.consğ”¹ _ _ _ _ _ _ _ _ HÏ„C HÏ„B
    have âŸ¨HÏ„â‚€, HÏ„â‚, _âŸ© := Hctx
    have âŸ¨HSÏ„â‚€, HSÏ„â‚âŸ© := log_approx_env.msubst.typing _ _ _ _ _ _ _ HÏ„â‚€ HÏ„â‚ HsemÎ“
    have HSÏ„Eâ‚€ := typing.congruence_under_ObsCtxâ„‚ _ _ _ _ _ _ HSÏ„â‚€ HCE
    have HSÏ„Eâ‚ := typing.congruence_under_ObsCtxâ„‚ _ _ _ _ _ _ HSÏ„â‚ HCE
    have âŸ¨Hlcâ‚€, Hclosedâ‚€âŸ© := typing.wf _ _ _ _ _ HSÏ„â‚€
    have âŸ¨Hlcâ‚, Hclosedâ‚âŸ© := typing.wf _ _ _ _ _ HSÏ„â‚
    simp at Hlcâ‚€ Hlcâ‚ Hclosedâ‚€ Hclosedâ‚
    --
    --
    -- EâŸ¦(x â†¦ v, Î³)eâ‚€âŸ§â‡“
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- EâŸ¦Î»x.Î³(eâ‚€) @ vâŸ§â‡“
    have Hterminationâ‚€ : termination (E (msubst Î³ (({0 â†¤ List.length Î“}eâ‚€).lam.appâ‚ v))) :=
      by
      have âŸ¨vâ‚€, Hvalueâ‚€, Hstepâ‚€âŸ© := Hterminationâ‚€
      exists vâ‚€
      constructor
      . apply Hvalueâ‚€
      . apply stepn.multi _ _ _ _ Hstepâ‚€
        apply step_grounded.congruence_under_ctxğ”¼ _ _ _ HE (typing.dynamic_impl_grounded _ _ _ _ HSÏ„â‚€)
        apply step_lvl.pure _ _ _ ctxğ•„.hole
        . simp [Hlcâ‚€]
        . simp [HEq, HEqSubstâ‚€]
          apply head.appâ‚; rw [identity.msubst]
          apply Hvalue; apply typing.closed_at_env _ _ _ _ _ HÏ„v
    --
    --
    -- EâŸ¦Î»x.Î³(eâ‚) @ vâŸ§â‡“
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- EâŸ¦(x â†¦ v, Î³)eâ‚âŸ§â‡“
    have âŸ¨vâ‚, Hvalueâ‚, Hstepâ‚âŸ© := IH _ _ Hctx _ _ HE HCE Hterminationâ‚€
    exists vâ‚
    constructor
    . apply Hvalueâ‚
    . have âŸ¨j, Hstepâ‚âŸ© := stepn_impl_stepn_indexed _ _ Hstepâ‚
      have âŸ¨_, _, vğ•–, _, Hvalueğ•–, Hstepğ•–â‚, HstepEâ‚âŸ© := stepn_indexed.refine_at_ctxğ”¼ _ _ _ _ HE Hvalueâ‚ (typing.dynamic_impl_grounded _ _ _ _ HSÏ„Eâ‚) Hstepâ‚
      simp at Hstepğ•–â‚
      have HvalueFun : value (msubst Î³ {0 â†¤ List.length Î“}eâ‚).lam := value.lam _ Hlcâ‚.left
      have HvalueArg : value (msubst Î³ v) := by rw [identity.msubst _ _ (typing.closed_at_env _ _ _ _ _ HÏ„v)]; apply Hvalue
      have âŸ¨_, _, Hstepğ•–â‚âŸ© := stepn_indexed.refine.appâ‚.eliminator _ _ _ _ HvalueFun HvalueArg Hvalueğ•– Hstepğ•–â‚
      have Hstepğ•–â‚ := stepn_indexed_impl_stepn _ _ _ Hstepğ•–â‚
      have HstepEâ‚ := stepn_indexed_impl_stepn _ _ _ HstepEâ‚
      apply stepn.trans _ _ _ _ HstepEâ‚
      simp [HEq, HEqSubstâ‚]
      apply stepn_grounded.congruence_under_ctxğ”¼ _ _ _ HE (
        by
        have HGâ‚ := typing.dynamic_impl_grounded _ _ _ _ HSÏ„â‚
        simp at HGâ‚
        apply grounded.under_opening_value
        . apply HGâ‚.right
        . apply HGâ‚.left
      ) Hstepğ•–â‚

lemma ciu_approx_respects_log_approx_value :
  âˆ€ k vâ‚€ vâ‚ vâ‚‚ Ï„,
    value vâ‚€ â†’ value vâ‚ â†’ value vâ‚‚ â†’
    log_approx_value k vâ‚€ vâ‚ Ï„ â†’
    ciu_approx â¦° vâ‚ vâ‚‚ Ï„ â†’
    log_approx_value k vâ‚€ vâ‚‚ Ï„ :=
  by
  intros k vâ‚€ vâ‚ vâ‚‚ Ï„ Hvalueâ‚€ Hvalueâ‚ Hvalueâ‚‚ Hsem_value Hciu
  induction Ï„ generalizing k vâ‚€ vâ‚ vâ‚‚
  case nat =>
    have âŸ¨HÏ„â‚, HÏ„â‚‚, Hciu_valueâŸ© := Hciu
    cases Hvalueâ‚€ <;> try simp at Hsem_value
    case lit nâ‚€ =>
    cases Hvalueâ‚ <;> try contradiction
    case lit nâ‚ =>
    cases Hvalueâ‚‚ <;> try contradiction
    case lit nâ‚‚ =>
    simp at Hsem_value
    cases Hn : compare nâ‚ nâ‚‚ with
    | eq =>
      rw [compare_eq_iff_eq] at Hn
      simp [Hsem_value, Hn]
    | lt =>
      exfalso; apply divergence
      rw [compare_lt_iff_lt] at Hn
      --
      --
      -- nâ‚ < nâ‚‚
      -- E = fun X => if (X - nâ‚) then 0 else diverge
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- EâŸ¦nâ‚âŸ§ â‡* 0
      -- EâŸ¦nâ‚‚âŸ§ â‡* diverge
      -- â¦° âŠ¢ EâŸ¦â¦° âŠ¢ â„•âŸ§ : â„•
      have Hstepâ‚ : ((.ifzâ‚ (.binaryâ‚ .sub (.lit nâ‚) (.lit nâ‚)) (.lit 0) diverge) â‡* (.lit 0)) :=
        by
        -- head sub
        apply stepn.multi
        apply step_lvl.pure (fun X => .ifzâ‚ X (.lit 0) diverge)
        . apply ctxğ•„.consğ”¹ _ _ (ctxğ”¹.ifzâ‚ _ _ (by simp) (typing.regular _ _ _ _ _ typing_diverge)) ctxğ•„.hole
        . simp
        . apply head.binaryâ‚
        -- head if then
        apply stepn.multi _ _ _ _ (stepn.refl _)
        apply step_lvl.pure _ _ _ ctxğ•„.hole
        . simp; apply typing.regular _ _ _ _ _ typing_diverge
        . simp; apply head.ifzâ‚_then
      have Hstepâ‚‚ : ((.ifzâ‚ (.binaryâ‚ .sub (.lit nâ‚‚) (.lit nâ‚)) (.lit 0) diverge) â‡* diverge) :=
        by
        -- head sub
        apply stepn.multi
        apply step_lvl.pure (fun X => .ifzâ‚ X (.lit 0) diverge)
        . apply ctxğ•„.consğ”¹ _ _ (ctxğ”¹.ifzâ‚ _ _ (by simp) (typing.regular _ _ _ _ _ typing_diverge)) ctxğ•„.hole
        . simp
        . apply head.binaryâ‚
        -- head if else
        apply stepn.multi _ _ _ _ (stepn.refl _)
        apply step_lvl.pure _ _ _ ctxğ•„.hole
        . simp; apply typing.regular _ _ _ _ _ typing_diverge
        . have âŸ¨n, HEqnâŸ© : âˆƒ n, nâ‚‚ - nâ‚ = n + 1 := by exists nâ‚‚ - nâ‚ - 1; omega
          simp [HEqn]
          apply head.ifzâ‚_else
      have HE : ctxğ”¼ (fun X => .ifzâ‚ (.binaryâ‚ .sub X (.lit nâ‚)) (.lit 0) diverge) :=
        by
        apply ctxğ”¼.consğ”¹ _ _ (ctxğ”¹.ifzâ‚ _ _ (by simp) (typing.regular _ _ _ _ _ typing_diverge))
        apply ctxğ”¼.consğ”¹ _ _ (ctxğ”¹.binarylâ‚ _ _ (by simp))
        apply ctxğ”¼.hole
      have HÏ„E : ObsCtxâ„‚ â¦° .nat (fun X => .ifzâ‚ (.binaryâ‚ .sub X (.lit nâ‚)) (.lit 0) diverge) â¦° .nat :=
        by
        have HÏ„C := ObsCtxâ„‚.hole â¦° .nat
        have HÏ„B := ObsCtxğ”¹.ifzâ‚ _ _ _ _ (typing.lit _ _ 0) typing_diverge
        have HÏ„C := ObsCtxâ„‚.consğ”¹ _ _ _ _ _ _ _ _ HÏ„C HÏ„B
        have HÏ„B := ObsCtxğ”¹.binarylâ‚ â¦° .sub _ (typing.lit _ _ nâ‚)
        apply ObsCtxâ„‚.consğ”¹ _ _ _ _ _ _ _ _ HÏ„C HÏ„B
      --
      --
      -- EâŸ¦nâ‚âŸ§â‡“
      -- â¦° âŠ§ nâ‚ â‰¤ğ‘ğ‘–ğ‘¢ nâ‚‚ : â„•
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- EâŸ¦nâ‚‚âŸ§â‡“
      have Htermination := Hciu_value _ typing.subst.nil _ _ HE HÏ„E (by exists .lit 0)
      --
      --
      -- EâŸ¦nâ‚‚âŸ§â‡“
      -- EâŸ¦nâ‚‚âŸ§ â‡* diverge
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- divergeâ‡“
      rw [â† termination.under_stepn]
      apply Htermination
      apply Hstepâ‚‚
    | gt =>
      exfalso; apply divergence
      rw [compare_gt_iff_gt] at Hn
      --
      --
      -- nâ‚ > nâ‚‚
      -- E = fun X => if (X - nâ‚‚) then diverge else 0
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- EâŸ¦nâ‚âŸ§ â‡* 0
      -- EâŸ¦nâ‚‚âŸ§ â‡* diverge
      -- â¦° âŠ¢ EâŸ¦â¦° âŠ¢ â„•âŸ§ : â„•
      have Hstepâ‚ : ((.ifzâ‚ (.binaryâ‚ .sub (.lit nâ‚) (.lit nâ‚‚)) diverge (.lit 0)) â‡* (.lit 0)) :=
        by
        -- head sub
        apply stepn.multi
        apply step_lvl.pure (fun X => .ifzâ‚ X diverge (.lit 0))
        . apply ctxğ•„.consğ”¹ _ _ (ctxğ”¹.ifzâ‚ _ _ (typing.regular _ _ _ _ _ typing_diverge) (by simp)) ctxğ•„.hole
        . simp
        . apply head.binaryâ‚
        -- head if then
        apply stepn.multi _ _ _ _ (stepn.refl _)
        apply step_lvl.pure _ _ _ ctxğ•„.hole
        . simp; apply typing.regular _ _ _ _ _ typing_diverge
        . have âŸ¨n, HEqnâŸ© : âˆƒ n, nâ‚ - nâ‚‚ = n + 1 := by exists nâ‚ - nâ‚‚ - 1; omega
          simp [HEqn]
          apply head.ifzâ‚_else
      have Hstepâ‚‚ : ((.ifzâ‚ (.binaryâ‚ .sub (.lit nâ‚‚) (.lit nâ‚‚)) diverge (.lit 0)) â‡* diverge) :=
        by
        -- head sub
        apply stepn.multi
        apply step_lvl.pure (fun X => .ifzâ‚ X diverge (.lit 0))
        . apply ctxğ•„.consğ”¹ _ _ (ctxğ”¹.ifzâ‚ _ _ (typing.regular _ _ _ _ _ typing_diverge) (by simp)) ctxğ•„.hole
        . simp
        . apply head.binaryâ‚
        -- head if else
        apply stepn.multi _ _ _ _ (stepn.refl _)
        apply step_lvl.pure _ _ _ ctxğ•„.hole
        . simp; apply typing.regular _ _ _ _ _ typing_diverge
        . simp; apply head.ifzâ‚_then
      have HE : ctxğ”¼ (fun X => .ifzâ‚ (.binaryâ‚ .sub X (.lit nâ‚‚)) diverge (.lit 0)) :=
        by
        apply ctxğ”¼.consğ”¹ _ _ (ctxğ”¹.ifzâ‚ _ _ (typing.regular _ _ _ _ _ typing_diverge) (by simp))
        apply ctxğ”¼.consğ”¹ _ _ (ctxğ”¹.binarylâ‚ _ _ (by simp))
        apply ctxğ”¼.hole
      have HÏ„E : ObsCtxâ„‚ â¦° .nat (fun X => .ifzâ‚ (.binaryâ‚ .sub X (.lit nâ‚‚)) diverge (.lit 0)) â¦° .nat :=
        by
        have HÏ„C := ObsCtxâ„‚.hole â¦° .nat
        have HÏ„B := ObsCtxğ”¹.ifzâ‚ _ _ _ _ typing_diverge (typing.lit _ _ 0)
        have HÏ„C := ObsCtxâ„‚.consğ”¹ _ _ _ _ _ _ _ _ HÏ„C HÏ„B
        have HÏ„B := ObsCtxğ”¹.binarylâ‚ â¦° .sub _ (typing.lit _ _ nâ‚‚)
        apply ObsCtxâ„‚.consğ”¹ _ _ _ _ _ _ _ _ HÏ„C HÏ„B
      --
      --
      -- EâŸ¦nâ‚âŸ§â‡“
      -- â¦° âŠ§ nâ‚ â‰¤ğ‘ğ‘–ğ‘¢ nâ‚‚ : â„•
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- EâŸ¦nâ‚‚âŸ§â‡“
      have Htermination := Hciu_value _ typing.subst.nil _ _ HE HÏ„E (by exists .lit 0)
      --
      --
      -- EâŸ¦nâ‚‚âŸ§â‡“
      -- EâŸ¦nâ‚‚âŸ§ â‡* diverge
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- divergeâ‡“
      rw [â† termination.under_stepn]
      apply Htermination
      apply Hstepâ‚‚
  case arrow Ï„ğ•’ Ï„ğ•“ Ï† IHğ•’ IHğ•“ =>
    have âŸ¨HÏ„â‚, HÏ„â‚‚, Hciu_valueâŸ© := Hciu
    cases Ï† <;> try simp at Hsem_value
    cases Hvalueâ‚€ <;> try simp at Hsem_value
    case lam eâ‚€ _ =>
    cases Hvalueâ‚ <;> try contradiction
    case lam eâ‚ _ =>
    cases Hvalueâ‚‚ <;> try contradiction
    case lam eâ‚‚ _ =>
    simp only [log_approx_value]
    constructor; simp only [log_approx_value] at Hsem_value; apply Hsem_value.left
    constructor; apply HÏ„â‚‚
    intros j Hindexj argvâ‚€ argvâ‚ Hsem_value_arg
    have âŸ¨HvalueArgâ‚€, HvalueArgâ‚âŸ© := log_approx_value.syntactic.value _ _ _ _ Hsem_value_arg
    have âŸ¨HÏ„Argâ‚€, HÏ„Argâ‚âŸ© := log_approx_value.syntactic.typing _ _ _ _ Hsem_value_arg
    simp only [log_approx_expr]
    intros i Hindexi vâ‚€ Hvalueâ‚€ Hstepâ‚€
    --
    --
    -- (Î»x.eâ‚€, Î»x.eâ‚) âˆˆ ğ“¥âŸ¦Ï„ğ•’ â†’ Ï„ğ•“âŸ§{k}
    -- (argvâ‚€, argvâ‚) âˆˆ ğ“¥âŸ¦Ï„ğ•’âŸ§{j}
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- (Î»x.eâ‚€ @ argvâ‚€, Î»x.eâ‚ @ argvâ‚) âˆˆ ğ“”âŸ¦Ï„ğ•“âŸ§{j}
    have Hsem_expr := log_approx_value.apply j _ _ _ _ _ _ (log_approx_value.antimono _ _ _ _ _ Hsem_value (by omega)) Hsem_value_arg
    simp only [log_approx_expr] at Hsem_expr
    --
    --
    -- Î»x.eâ‚€ @ argvâ‚€ â‡ âŸ¦iâŸ§ vâ‚€
    -- (Î»x.eâ‚€ @ argvâ‚€, Î»x.eâ‚ @ argvâ‚) âˆˆ ğ“”âŸ¦Ï„ğ•“âŸ§{j}
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- Î»x.eâ‚ @ argvâ‚ â‡* vâ‚
    -- (vâ‚€, vâ‚) âˆˆ ğ“¥âŸ¦Ï„ğ•“âŸ§{j - i}
    have âŸ¨vâ‚, Hstepâ‚, Hsem_valueâŸ© := Hsem_expr i Hindexi vâ‚€ Hvalueâ‚€ Hstepâ‚€
    have âŸ¨Hvalueâ‚€, Hvalueâ‚âŸ© := log_approx_value.syntactic.value _ _ _ _ Hsem_value
    have âŸ¨HÏ„vâ‚€, HÏ„vâ‚âŸ© := log_approx_value.syntactic.typing _ _ _ _ Hsem_value
    --
    --
    -- â¦° âŠ¢ (fun X => X @ argvâ‚)âŸ¦â¦° âŠ¢ Ï„ğ•’ â†’ Ï„ğ•“âŸ§ : Ï„ğ•“
    -- â¦° âŠ§ Î»x.eâ‚ â‰¤ğ‘ğ‘–ğ‘¢ Î»x.eâ‚‚ : Ï„ğ•’ â†’ Ï„ğ•“
    -- Î»x.eâ‚ @ argvâ‚ â‡* vâ‚
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- Î»x.eâ‚‚ @ argvâ‚ â‡* vâ‚‚
    have HE : ctxğ”¼ (fun X => .appâ‚ X argvâ‚) := ctxğ”¼.consğ”¹ _ _ (ctxğ”¹.applâ‚ _ (lc.value _ HvalueArgâ‚)) ctxğ”¼.hole
    have HÏ„E : ObsCtxâ„‚ â¦° (Ï„ğ•’.arrow Ï„ğ•“ âŠ¥) (fun X => .appâ‚ X argvâ‚) â¦° Ï„ğ•“ := ObsCtxâ„‚.consğ”¹ _ _ _ _ _ _ _ _ (ObsCtxâ„‚.hole _ _) (ObsCtxğ”¹.applâ‚ _ _ _ _ HÏ„Argâ‚)
    have âŸ¨vâ‚‚, Hvalueâ‚‚, Hstepâ‚‚âŸ© := Hciu_value _ typing.subst.nil _ _ HE HÏ„E (by exists vâ‚)
    --
    --
    -- â¦° âŠ¢ Î»x.eâ‚‚ : Ï„ğ•’ â†’ Ï„ğ•“
    -- â¦° âŠ¢ argvâ‚ : Ï„ğ•’
    -- Î»x.eâ‚‚ @ argvâ‚ â‡* vâ‚‚
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- â¦° âŠ¢ vâ‚‚ : Ï„ğ•“
    have HÏ„vâ‚‚ : typing â¦° ğŸš vâ‚‚ Ï„ğ•“ âŠ¥ :=
      by
      apply preservation.dynamic _ _ _ Hstepâ‚‚
      rw [â† Effect.union_pure âŠ¥, â† Effect.union_pure (âŠ¥ âˆª âŠ¥)]
      apply typing.appâ‚; apply HÏ„â‚‚; apply HÏ„Argâ‚
    exists vâ‚‚; constructor
    . apply Hstepâ‚‚
    . apply IHğ•“ _ _ _ _ Hvalueâ‚€ Hvalueâ‚ Hvalueâ‚‚ Hsem_value
      constructor; apply HÏ„vâ‚
      constructor; apply HÏ„vâ‚‚
      intros Î³ HÎ³ E Ï„ğ•– HE HÏ„E Hterminationâ‚
      cases Î³ <;> cases HÎ³
      --
      --
      -- Î»x.eâ‚ @ argvâ‚ â‡* vâ‚
      -- EâŸ¦vâ‚âŸ§â‡“
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- EâŸ¦Î»x.eâ‚ @ argvâ‚âŸ§â‡“
      have Hterminationâ‚ : termination EâŸ¦.appâ‚ (.lam eâ‚) argvâ‚âŸ§ :=
        by
        rw [termination.under_stepn]
        apply Hterminationâ‚
        apply stepn_grounded.congruence_under_ctxğ”¼ _ _ _ HE (
          by
          constructor
          . apply typing.dynamic_impl_grounded _ _ _ _ HÏ„â‚
          . apply typing.dynamic_impl_grounded _ _ _ _ HÏ„Argâ‚
        ) Hstepâ‚
      --
      --
      -- â¦° âŠ¢ EâŸ¦â¦° âŠ¢ Ï„ğ•“âŸ§ : Ï„ğ•–
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- â¦° âŠ¢ (E âˆ˜ fun X => X @ argvâ‚)âŸ¦â¦° âŠ¢ Ï„ğ•’ â†’ Ï„ğ•“âŸ§ : Ï„ğ•–
      have HEApp : ctxğ”¼ (E âˆ˜ fun X => .appâ‚ X argvâ‚) := compose.ctxğ”¼_ctxğ”¹ _ _ HE (ctxğ”¹.applâ‚ _ (lc.value _ HvalueArgâ‚))
      have HÏ„EApp : ObsCtxâ„‚ â¦° (Ï„ğ•’.arrow Ï„ğ•“ âŠ¥) (E âˆ˜ fun X => .appâ‚ X argvâ‚) â¦° Ï„ğ•– := ObsCtxâ„‚.consğ”¹ _ _ _ _ _ _ _ _ HÏ„E (ObsCtxğ”¹.applâ‚ _ _ _ _ HÏ„Argâ‚)
      --
      --
      -- â¦° âŠ¢ (E âˆ˜ fun X => X @ argvâ‚)âŸ¦â¦° âŠ¢ Ï„ğ•’ â†’ Ï„ğ•“âŸ§ : Ï„ğ•–
      -- â¦° âŠ§ Î»x.eâ‚ â‰¤ğ‘ğ‘–ğ‘¢ Î»x.eâ‚‚ : Ï„ğ•’ â†’ Ï„ğ•“
      -- EâŸ¦Î»x.eâ‚ @ argvâ‚âŸ§â‡“
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- EâŸ¦Î»x.eâ‚‚ @ argvâ‚âŸ§â‡“
      have Hterminationâ‚‚ := Hciu_value _ typing.subst.nil _ _ HEApp HÏ„EApp Hterminationâ‚
      --
      --
      -- EâŸ¦Î»x.eâ‚‚ @ argvâ‚âŸ§â‡“
      -- Î»x.eâ‚‚ @ argvâ‚ â‡* vâ‚‚
      -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
      -- EâŸ¦vâ‚‚âŸ§â‡“
      rw [â† termination.under_stepn]
      apply Hterminationâ‚‚; apply stepn_grounded.congruence_under_ctxğ”¼ _ _ _ HE (
        by
        constructor
        . apply typing.dynamic_impl_grounded _ _ _ _ HÏ„â‚‚
        . apply typing.dynamic_impl_grounded _ _ _ _ HÏ„Argâ‚
      ) Hstepâ‚‚
  case fragment => simp at Hsem_value
  case rep => simp at Hsem_value

-- Î“ âŠ§ eâ‚€ â‰¤ğ‘ğ‘–ğ‘¢ eâ‚ : Ï„
-- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
-- Î“ âŠ§ eâ‚€ â‰¤ğ‘™ğ‘œğ‘” eâ‚ : Ï„
theorem ciu_approx_impl_log_approx :
  âˆ€ Î“ Ï„ eâ‚€ eâ‚,
    ciu_approx Î“ eâ‚€ eâ‚ Ï„ â†’
    log_approx Î“ eâ‚€ eâ‚ Ï„ :=
  by
  intros Î“ Ï„ eâ‚€ eâ‚ Hciu
  have âŸ¨HÏ„â‚€, HÏ„â‚, HciuâŸ© := Hciu
  constructor; apply HÏ„â‚€
  constructor; apply HÏ„â‚
  intros k Î³â‚€ Î³â‚ HsemÎ“
  have âŸ¨HÎ³â‚€, HÎ³â‚âŸ© := log_approx_env.syntactic.typing _ _ _ _ HsemÎ“
  simp only [log_approx_expr]
  intros j Hj vâ‚€ Hvalueâ‚€ Hstepâ‚€
  --
  --
  -- Î³â‚€(eâ‚€) â‡ âŸ¦jâŸ§ vâ‚€
  -- Î“ âŠ¢ eâ‚€ : Ï„
  -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
  -- Î³â‚(eâ‚€) â‡* vâ‚
  -- (vâ‚€, vâ‚) âˆˆ ğ“¥âŸ¦Ï„âŸ§{k - j}
  have âŸ¨_, _, Heâ‚€âŸ© := log_approx.fundamental _ _ _ HÏ„â‚€
  have Hsem_expr := Heâ‚€ _ _ _ HsemÎ“
  have âŸ¨HSÎ³â‚€Ï„â‚€, HSÎ³â‚Ï„â‚€âŸ© := log_approx_env.msubst.typing _ _ _ _ _ _ _ HÏ„â‚€ HÏ„â‚€ HsemÎ“
  have âŸ¨HSÎ³â‚€Ï„â‚, HSÎ³â‚Ï„â‚âŸ© := log_approx_env.msubst.typing _ _ _ _ _ _ _ HÏ„â‚ HÏ„â‚ HsemÎ“
  rw [log_approx_expr] at Hsem_expr
  have âŸ¨vâ‚, Hstepâ‚, Hsem_valueâŸ© := Hsem_expr _ Hj _ Hvalueâ‚€ Hstepâ‚€
  have âŸ¨Hvalueâ‚€, Hvalueâ‚âŸ© := log_approx_value.syntactic.value _ _ _ _ Hsem_value
  have âŸ¨HÏ„vâ‚€, HÏ„vâ‚âŸ© := log_approx_value.syntactic.typing _ _ _ _ Hsem_value
  --
  --
  -- Î³â‚(eâ‚€) â‡* vâ‚
  -- Î“ âŠ§ eâ‚€ â‰¤ğ‘ğ‘–ğ‘¢ eâ‚ : Ï„
  -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
  -- Î³â‚(eâ‚) â‡* vâ‚‚
  have âŸ¨vâ‚‚, Hvalueâ‚‚, Hstepâ‚‚âŸ© := Hciu _ HÎ³â‚ _ _ ctxğ”¼.hole (ObsCtxâ„‚.hole _ _) (by exists vâ‚)
  --
  --
  -- Î³â‚(eâ‚) â‡* vâ‚‚
  -- â¦° âŠ¢ Î³â‚(eâ‚) : Ï„
  -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
  -- â¦° âŠ¢ vâ‚‚ : Ï„
  have HÏ„vâ‚‚ : typing â¦° ğŸš vâ‚‚ Ï„ âŠ¥ := preservation.dynamic _ _ _ Hstepâ‚‚ HSÎ³â‚Ï„â‚
  exists vâ‚‚; constructor
  . apply Hstepâ‚‚
  . apply ciu_approx_respects_log_approx_value _ _ _ _ _ Hvalueâ‚€ Hvalueâ‚ Hvalueâ‚‚ Hsem_value
    constructor; apply HÏ„vâ‚
    constructor; apply HÏ„vâ‚‚
    intros Î³ HÎ³ E Ï„ğ•– HE HÏ„E Hterminationâ‚
    cases Î³ <;> cases HÎ³
    --
    --
    -- Î³â‚(eâ‚€) â‡* vâ‚
    -- EâŸ¦vâ‚âŸ§â‡“
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- EâŸ¦Î³â‚(eâ‚€)âŸ§â‡“
    have Hterminationâ‚ : termination EâŸ¦msubst Î³â‚ eâ‚€âŸ§ :=
      by
      rw [termination.under_stepn]
      apply Hterminationâ‚
      apply stepn_grounded.congruence_under_ctxğ”¼ _ _ _ HE (typing.dynamic_impl_grounded _ _ _ _ HSÎ³â‚Ï„â‚€) Hstepâ‚
    --
    --
    -- EâŸ¦Î³â‚(eâ‚€)âŸ§â‡“
    -- Î“ âŠ§ eâ‚€ â‰¤ğ‘ğ‘–ğ‘¢ eâ‚ : Ï„
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- EâŸ¦Î³â‚(eâ‚)âŸ§â‡“
    have Hterminationâ‚‚ := Hciu _ HÎ³â‚ _ _ HE HÏ„E Hterminationâ‚
    --
    --
    -- EâŸ¦Î³â‚(eâ‚)âŸ§â‡“
    -- Î³â‚(eâ‚) â‡* vâ‚‚
    -- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
    -- EâŸ¦vâ‚‚âŸ§â‡“
    rw [â† termination.under_stepn]
    apply Hterminationâ‚‚
    apply stepn_grounded.congruence_under_ctxğ”¼ _ _ _ HE (typing.dynamic_impl_grounded _ _ _ _ HSÎ³â‚Ï„â‚) Hstepâ‚‚

-- Î“ âŠ§ eâ‚€ â‰¤ğ‘ğ‘¡ğ‘¥ eâ‚ : Ï„
-- â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”â€”
-- Î“ âŠ§ eâ‚€ â‰¤ğ‘™ğ‘œğ‘” eâ‚ : Ï„
theorem log_approx.completeness :
  âˆ€ Î“ Ï„ eâ‚€ eâ‚,
    ctx_approx Î“ eâ‚€ eâ‚ Ï„ â†’
    log_approx Î“ eâ‚€ eâ‚ Ï„ :=
  by
  intros Î“ Ï„ eâ‚€ eâ‚ Hctx
  apply ciu_approx_impl_log_approx
  apply ctx_approx_impl_ciu_approx
  apply Hctx
