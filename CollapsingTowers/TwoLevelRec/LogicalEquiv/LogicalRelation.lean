import CollapsingTowers.TwoLevelRec.SyntacticTyping.Defs

mutual
@[simp]
def log_rel_value : â„• â†’ Expr â†’ Expr â†’ Ty â†’ Prop
  --
  --
  -- ğ“¥âŸ¦â„•âŸ§â‚– â‰œ {(n, n) | n âˆˆ â„•}
  | _, .lit nâ‚€, .lit nâ‚, .nat => nâ‚€ = nâ‚
  --
  --
  -- ğ“¥âŸ¦Ï„ğ•’ â†’ Ï„ğ•“âŸ§â‚– â‰œ {(Î»x.eâ‚€, Î»x.eâ‚) |
  --   âˆ… âŠ¢ Î»x.eâ‚€ : Ï„ğ•’ â†’ Ï„ğ•“ âˆ§
  --   âˆ… âŠ¢ Î»x.eâ‚ : Ï„ğ•’ â†’ Ï„ğ•“ âˆ§
  --   âˆ€ j â‰¤ k, (vâ‚€, vâ‚) âˆˆ ğ“¥âŸ¦Ï„ğ•’âŸ§â±¼. (Î»x.eâ‚€ @ vâ‚€, Î»x.eâ‚ @ vâ‚) âˆˆ ğ“”âŸ¦Ï„ğ•“âŸ§â±¼
  -- }
  | k, .lam eâ‚€, .lam eâ‚, (.arrow Ï„ğ•’ Ï„ğ•“ .pure) =>
    typing [] ğŸš (.lam eâ‚€) (.arrow Ï„ğ•’ Ï„ğ•“ âˆ…) âˆ… âˆ§
    typing [] ğŸš (.lam eâ‚) (.arrow Ï„ğ•’ Ï„ğ•“ âˆ…) âˆ… âˆ§
    âˆ€ j, j â‰¤ k â†’
      âˆ€ vâ‚€ vâ‚,
        log_rel_value j vâ‚€ vâ‚ Ï„ğ•’ â†’
        log_rel_expr j (.appâ‚ (.lam eâ‚€) vâ‚€) (.appâ‚ (.lam eâ‚) vâ‚) Ï„ğ•“
  | _, _, _, _ => false

termination_by k _ _ Ï„ => (Ï„, k)
decreasing_by all_goals apply Prod.Lex.left; simp; omega

-- ğ“”âŸ¦Ï„âŸ§â‚– â‰œ {(eâ‚€, eâ‚) | âˆ€ j < k, vâ‚€. eâ‚€ â‡â±¼ vâ‚€ â†’ âˆƒ vâ‚, eâ‚ â‡* vâ‚ âˆ§ (vâ‚€, vâ‚) âˆˆ ğ“¥âŸ¦Ï„âŸ§â‚–â‚‹â±¼}
@[simp]
def log_rel_expr (k : â„•) (eâ‚€ : Expr) (eâ‚ : Expr) (Ï„ : Ty) : Prop :=
    âˆ€ j, j < k â†’
      âˆ€ vâ‚€, value vâ‚€ â†’ (eâ‚€ â‡ âŸ¦jâŸ§ vâ‚€) â†’
      âˆƒ vâ‚, (eâ‚ â‡* vâ‚) âˆ§ log_rel_value (k - j) vâ‚€ vâ‚ Ï„

termination_by (Ï„, k + 1)
decreasing_by apply Prod.Lex.right; omega
end

inductive log_rel_env : â„• â†’ Subst â†’ Subst â†’ TEnv â†’ Prop where
  | nil : âˆ€ k, log_rel_env k [] [] []
  | cons :
    âˆ€ k vâ‚€ Î³â‚€ vâ‚ Î³â‚ Ï„ Î“,
      log_rel_value k vâ‚€ vâ‚ Ï„ â†’
      log_rel_env k Î³â‚€ Î³â‚ Î“ â†’
      log_rel_env k (vâ‚€ :: Î³â‚€) (vâ‚ :: Î³â‚) ((Ï„, ğŸš) :: Î“)

-- Î“ âŠ§ eâ‚€ â‰¤ğ‘™ğ‘œğ‘” eâ‚ : Ï„ â‰œ
--   Î“ âŠ¢ Î»x.eâ‚€ : Ï„ğ•’ â†’ Ï„ğ•“ âˆ§
--   Î“ âŠ¢ Î»x.eâ‚€ : Ï„ğ•’ â†’ Ï„ğ•“ âˆ§
--   âˆ€ k â‰¥ 0, (Î³â‚€, Î³â‚) âˆˆ ğ“–âŸ¦Î“âŸ§â‚–. (Î³â‚€(eâ‚€), Î³â‚(eâ‚)) âˆˆ ğ“”âŸ¦Ï„âŸ§â‚–
@[simp]
def log_rel_typing (Î“ : TEnv) (eâ‚€ : Expr) (eâ‚ : Expr) (Ï„ : Ty) : Prop :=
  typing Î“ ğŸš eâ‚€ Ï„ âˆ… âˆ§
  typing Î“ ğŸš eâ‚ Ï„ âˆ… âˆ§
  âˆ€ k Î³â‚€ Î³â‚,
    log_rel_env k Î³â‚€ Î³â‚ Î“ â†’
    log_rel_expr k (multi_subst Î³â‚€ eâ‚€) (multi_subst Î³â‚ eâ‚) Ï„

-- Î“ âŠ§ eâ‚€ â‰ˆğ‘™ğ‘œğ‘” eâ‚ : Ï„ â‰œ Î“ âŠ§ eâ‚€ â‰¤ğ‘™ğ‘œğ‘” eâ‚ : Ï„ âˆ§ Î“ âŠ§ eâ‚ â‰¤ğ‘™ğ‘œğ‘” eâ‚€ : Ï„
@[simp]
def log_equiv (Î“ : TEnv) (eâ‚€ : Expr) (eâ‚ : Expr) (Ï„ : Ty) : Prop :=
  log_rel_typing Î“ eâ‚€ eâ‚ Ï„ âˆ§ log_rel_typing Î“ eâ‚ eâ‚€ Ï„

lemma log_rel_value.antimono :
  âˆ€ kâ‚€ kâ‚ vâ‚€ vâ‚ Ï„,
    log_rel_value kâ‚€ vâ‚€ vâ‚ Ï„ â†’
    kâ‚ â‰¤ kâ‚€ â†’
    log_rel_value kâ‚ vâ‚€ vâ‚ Ï„ :=
  by
  intros kâ‚€ kâ‚ vâ‚€ vâ‚ Ï„ Hsem_value HLe
  cases Ï„
  case nat =>
    cases vâ‚€ <;> cases vâ‚ <;> simp at *
    omega
  case arrow Ï„ğ•’ Ï„ğ•“ Ï† =>
    cases vâ‚€ <;> try simp at Hsem_value
    case lam eâ‚€ =>
    cases vâ‚ <;> try simp at Hsem_value
    case lam eâ‚ =>
    cases Ï†
    case reify => simp at Hsem_value
    case pure =>
      simp only [log_rel_value] at Hsem_value
      have âŸ¨HÏ„â‚€, HÏ„â‚, Hsem_value_lamâŸ© := Hsem_value
      simp only [log_rel_value]
      constructor; apply HÏ„â‚€
      constructor; apply HÏ„â‚
      intros j HLe; apply Hsem_value_lam; omega
  case fragment => simp at Hsem_value
  case rep => simp at Hsem_value

lemma log_rel_expr.antimono :
  âˆ€ kâ‚€ kâ‚ eâ‚€ eâ‚ Ï„,
    log_rel_expr kâ‚€ eâ‚€ eâ‚ Ï„ â†’
    kâ‚ â‰¤ kâ‚€ â†’
    log_rel_expr kâ‚ eâ‚€ eâ‚ Ï„ :=
  by
  intros kâ‚€ kâ‚ eâ‚€ eâ‚ Ï„ Hsem_expr HLe
  simp only [log_rel_expr]
  simp only [log_rel_expr] at Hsem_expr
  intros j Hindex vâ‚€ Hvalueâ‚€ Hstepâ‚€
  have âŸ¨vâ‚, Hstepâ‚, Hsem_valueâŸ© := Hsem_expr j (by omega) vâ‚€ Hvalueâ‚€ Hstepâ‚€
  exists vâ‚
  constructor; apply Hstepâ‚
  apply log_rel_value.antimono
  apply Hsem_value; omega

lemma log_rel_env.antimono :
  âˆ€ kâ‚€ kâ‚ Î³â‚€ Î³â‚ Î“,
    log_rel_env kâ‚€ Î³â‚€ Î³â‚ Î“ â†’
    kâ‚ â‰¤ kâ‚€ â†’
    log_rel_env kâ‚ Î³â‚€ Î³â‚ Î“ :=
  by
  intros kâ‚€ kâ‚ Î³â‚€ Î³â‚ Î“ HsemÎ“ HLe
  induction HsemÎ“
  case nil => apply log_rel_env.nil
  case cons Hsem_value _ IH =>
    apply log_rel_env.cons
    apply log_rel_value.antimono; apply Hsem_value; apply HLe
    apply IH

lemma log_rel_value.syntactic.value :
  âˆ€ k vâ‚€ vâ‚ Ï„,
    log_rel_value k vâ‚€ vâ‚ Ï„ â†’
    value vâ‚€ âˆ§ value vâ‚ :=
  by
  intros k vâ‚€ vâ‚ Ï„ Hsem_value
  cases Ï„
  case nat =>
    cases vâ‚€ <;> cases vâ‚ <;> simp at Hsem_value
    constructor
    apply value.lit
    apply value.lit
  case arrow Ï† =>
    cases vâ‚€ <;> cases vâ‚ <;> cases Ï† <;> simp at Hsem_value
    have âŸ¨HÏ„â‚€, HÏ„â‚, _âŸ© := Hsem_value
    constructor
    apply value.lam; apply typing.regular; apply HÏ„â‚€
    apply value.lam; apply typing.regular; apply HÏ„â‚
  all_goals simp at Hsem_value

lemma log_rel_value.syntactic.typing :
  âˆ€ k vâ‚€ vâ‚ Ï„,
    log_rel_value k vâ‚€ vâ‚ Ï„ â†’
    typing [] ğŸš vâ‚€ Ï„ âˆ… âˆ§ typing [] ğŸš vâ‚ Ï„ âˆ… :=
  by
  intros k vâ‚€ vâ‚ Ï„ Hsem_value
  cases Ï„
  case nat =>
    cases vâ‚€ <;> cases vâ‚ <;> simp at Hsem_value
    constructor; apply typing.lit; apply typing.lit
  case arrow Ï† =>
    cases vâ‚€ <;> cases vâ‚ <;> cases Ï† <;> simp at Hsem_value
    have âŸ¨HÏ„â‚€, HÏ„â‚, _âŸ© := Hsem_value
    constructor; apply HÏ„â‚€; apply HÏ„â‚
  all_goals simp at Hsem_value

lemma log_rel_value.apply :
  âˆ€ k fâ‚€ argâ‚€ fâ‚ argâ‚ Ï„ğ•’ Ï„ğ•“,
    log_rel_value k fâ‚€ fâ‚ (.arrow Ï„ğ•’ Ï„ğ•“ âˆ…) â†’
    log_rel_value k argâ‚€ argâ‚ Ï„ğ•’ â†’
    log_rel_expr k (.appâ‚ fâ‚€ argâ‚€) (.appâ‚ fâ‚ argâ‚) Ï„ğ•“ :=
  by
  intros k fâ‚€ argâ‚€ fâ‚ argâ‚ Ï„ğ•’ Ï„ğ•“ Hsem_value_fun Hsem_value_arg
  cases fâ‚€ <;> cases fâ‚ <;> simp only [log_rel_value] at Hsem_value_fun <;> try contradiction
  have âŸ¨_, _, Hsem_value_funâŸ© := Hsem_value_fun
  apply Hsem_value_fun; rfl; apply Hsem_value_arg

lemma log_rel_env.length :
  âˆ€ k Î³â‚€ Î³â‚ Î“,
    log_rel_env k Î³â‚€ Î³â‚ Î“ â†’
    Î³â‚€.length = Î“.length âˆ§
    Î³â‚.length = Î“.length :=
  by
  intros k Î³â‚€ Î³â‚ Î“ H
  induction H
  case nil => simp
  case cons IH =>
    constructor
    . simp; apply IH.left
    . simp; apply IH.right

lemma log_rel_env.binds_log_rel_value :
  âˆ€ k Î³â‚€ Î³â‚ Î“ x Ï„,
    log_rel_env k Î³â‚€ Î³â‚ Î“ â†’
    binds x (Ï„, ğŸš) Î“ â†’
    log_rel_value k (multi_subst Î³â‚€ (.fvar x)) (multi_subst Î³â‚ (.fvar x)) Ï„ :=
  by
  intros k Î³â‚€ Î³â‚ Î“ x Ï„ HsemÎ“ Hbinds
  induction HsemÎ“
  case nil => nomatch Hbinds
  case cons vâ‚€ Î³â‚€ vâ‚ Î³â‚ Ï„ Î“ Hsem_value HsemÎ“ IH =>
    have âŸ¨HÏ„â‚€, HÏ„â‚âŸ© := log_rel_value.syntactic.typing _ _ _ _ Hsem_value
    have âŸ¨HEqâ‚€, HEqâ‚âŸ© := log_rel_env.length _ _ _ _ HsemÎ“
    simp [HEqâ‚€, HEqâ‚]
    by_cases HEqx : Î“.length = x
    . simp [if_pos HEqx]
      simp [if_pos HEqx] at Hbinds
      rw [â† Hbinds, identity.multi_subst, identity.multi_subst]
      apply Hsem_value
      apply typing.closed_at_env []; apply HÏ„â‚
      apply typing.closed_at_env []; apply HÏ„â‚€
    . simp [if_neg HEqx]
      simp [if_neg HEqx] at Hbinds
      apply IH; apply Hbinds

lemma log_rel_env.multi_wf :
  âˆ€ k Î³â‚€ Î³â‚ Î“,
    log_rel_env k Î³â‚€ Î³â‚ Î“ â†’
    multi_wf Î³â‚€ âˆ§ multi_wf Î³â‚ :=
  by
  intros k Î³â‚€ Î³â‚ Î“ HsemÎ“
  induction HsemÎ“
  case nil => repeat constructor
  case cons Hsem_value _ IH =>
    have âŸ¨HÏ„â‚€, HÏ„â‚âŸ© := log_rel_value.syntactic.typing _ _ _ _ Hsem_value
    constructor
    . constructor
      apply typing.wf []; apply HÏ„â‚€
      apply IH.left
    . constructor
      apply typing.wf []; apply HÏ„â‚
      apply IH.right

lemma log_rel_env.multi_subst.typing :
  âˆ€ k Î³â‚€ Î³â‚ eâ‚€ eâ‚ Î“ Ï„,
    typing Î“ ğŸš eâ‚€ Ï„ âˆ… â†’
    typing Î“ ğŸš eâ‚ Ï„ âˆ… â†’
    log_rel_env k Î³â‚€ Î³â‚ Î“ â†’
    typing [] ğŸš (multi_subst Î³â‚€ eâ‚€) Ï„ âˆ… âˆ§
    typing [] ğŸš (multi_subst Î³â‚ eâ‚) Ï„ âˆ… :=
  by
  intros k Î³â‚€ Î³â‚ eâ‚€ eâ‚ Î“ Ï„ HÏ„â‚€ HÏ„â‚ HsemÎ“
  induction HsemÎ“ generalizing eâ‚€ eâ‚
  case nil => constructor; apply HÏ„â‚€; apply HÏ„â‚
  case cons Î“ Hsem_value HsemÎ“ IH =>
    have âŸ¨HEqâ‚€, HEqâ‚âŸ© := log_rel_env.length _ _ _ _ HsemÎ“
    have âŸ¨HÏ„vâ‚€, HÏ„vâ‚âŸ© := log_rel_value.syntactic.typing _ _ _ _ Hsem_value
    apply IH
    . rw [HEqâ‚€]; apply preservation.dyn_subst; rw [â† List.append_nil Î“]
      apply typing.weakening; apply HÏ„vâ‚€; apply HÏ„â‚€
    . rw [HEqâ‚]; apply preservation.dyn_subst; rw [â† List.append_nil Î“]
      apply typing.weakening; apply HÏ„vâ‚; apply HÏ„â‚
